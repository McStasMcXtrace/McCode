/*******************************************************************************
*
* McXrace, xray-tracing package
*         Copyright. All rights reserved
*         Risoe National Laboratory, Roskilde, Denmark
*         Institut Laue Langevin, Grenoble, France
*
* Component: Beamstop
*
* %ID
*
* Written by: Kristian Nielsen
* Modified by Erik BergbÃ¤ck Knudsen
* Date: March 2011
* Release: McXtrace 1.0rc1
* Origin: Risoe
*
* Rectangular/circular beam stop.
*
* %D
* A simple rectangular or circular beam stop.
* Infinitely thin and infinitely absorbing.
* The beam stop is by default rectangular. You may either
* specify the radius (circular shape), or the rectangular bounds.
*
* Example: Beamstop(xmin=-0.05, xmax=0.05, ymin=-0.05, ymax=0.05)
*          Beamstop(radius=0.1)
*
* %PAR
*
* INPUT PARAMETERS
*
* radius: radius of the beam stop in the z=0 plane, centered at Origo (m)
* xmin:   Lower x bound (m)
* xmax:   Upper x bound (m)
* ymin:   Lower y bound (m)
* ymax:   Upper y bound (m)
* xwidth: Width of beamstop (x). Overrides xmin,xmax. (m)
* yheight:Height of beamstop (y). Overrides ymin,ymax. (m)
*
* %END
*******************************************************************************/

DEFINE COMPONENT Beamstop
DEFINITION PARAMETERS ()
SETTING PARAMETERS (xmin=-0.05, xmax=0.05, ymin=-0.05, ymax=0.05,
  xwidth=0, yheight=0, radius=0)
OUTPUT PARAMETERS ()
/* X-ray parameters: (x,y,z,kx,ky,kz,phi,t,Ex,Ey,Ez,p) */ 

INITIALIZE
%{
  if (xwidth  > 0) { xmax = xwidth/2;  xmin = -xmax; }
  if (yheight > 0) { ymax = yheight/2; ymin = -ymax; }
  
  if (xmin == 0 && xmax == 0 && ymin == 0 & ymax == 0 && radius == 0)
  { fprintf(stderr,"Beamstop: %s: Error: give geometry\n", NAME_CURRENT_COMP); exit(-1); }
%}

TRACE
%{
    double dl,x0,y0,z0;
    ALLOW_BACKPROP;
    PROP_Z0;
    dl=sqrt((x-x0)*(x-x0)+(y-y0)*(y-y0)+(z-z0)*(z-z0));
    if ((dl>=0) && ((radius!=0) && (x*x + y*y <= radius*radius))
    || ((dl>=0) && (radius==0) && (x>xmin && x<xmax && y>ymin && y<ymax)))
      ABSORB;
    else
      RESTORE_XRAY(INDEX_CURRENT_COMP, x, y, z, kx, ky, kz, phi, t, Ex, Ey, Ez, p);
%}

MCDISPLAY
%{
  magnify("xy");
  if (radius != 0)
    circle("xy", 0, 0, 0, radius);
  else
    multiline(5, (double)xmin, (double)ymin, 0.0,
               (double)xmax, (double)ymin, 0.0,
               (double)xmax, (double)ymax, 0.0,
               (double)xmin, (double)ymax, 0.0,
               (double)xmin, (double)ymin, 0.0);
%}

END
