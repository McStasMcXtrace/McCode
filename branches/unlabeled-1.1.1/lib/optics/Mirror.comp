/*******************************************************************************
*
* McStas, version 1.0, released October 26, 1998
*         Maintained by Kristian Nielsen and Kim Lefmann,
*         Risoe National Laboratory, Roskilde, Denmark
*
* Component: Mirror
*
* Written by: KN, August 1998
* Modified by: KL, October 6, 1998
*
* Single mirror plate (used to build guides in compound components).
*
* INPUT PARAMETERS:
*
* xlenght: length of mirror plate (m)
* yheight: height of mirror plate (m)
* R0:      Low-angle reflectivity (1)
* Qc:      Critical scattering vector (AA-1)
* alpha:   Slope of reflectivity (AA)
* m:       m-value of material (1)
* W:       Width of supermirror cut-off (AA-1)
*
*******************************************************************************/

DEFINE COMPONENT Mirror
DEFINITION PARAMETERS (xlength, yheight,R0,Qc,alpha,m,W)
SETTING PARAMETERS ()
STATE PARAMETERS (x,y,z,vx,vy,vz,t,s1,s2,p)
TRACE
%{
  double dt, q;
  
  /* First check if neutron has the right direction. */
  if(vz != 0.0 && (dt = -z/vz) >= 0)
  {
    double old_x = x, old_y = y;
    x += vx*dt;
    y += vy*dt;
    /* Now check if neutron intersects mirror. */
    if(x >= 0 && x <= xlength && y >= 0 && y <= yheight)
    {
      z = 0;
      t += dt;
      q = fabs(2*vz*V2Q);
      vz = -vz;
      /* Reflectivity (see component Guide). */
      if(q > Qc)
      {
        double arg = (q-m*Qc)/W;
        if(arg < 10)
	  p *= .5*(1-tanh(arg))*(1-alpha*(q-Qc));
        else
          ABSORB;                               /* Cutoff ~ 1E-10 */
      }
    }
    else
    {
      /* No intersection: restore neutron position. */
      x = old_x;
      y = old_y;
    }
  }
%}
END
