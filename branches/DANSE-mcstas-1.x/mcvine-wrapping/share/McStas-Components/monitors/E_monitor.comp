/*******************************************************************************
*
* McStas, neutron ray-tracing package
*         Copyright (C) 1997-2007, All rights reserved
*         Risoe National Laboratory, Roskilde, Denmark
*         Institut Laue Langevin, Grenoble, France
*
* Component: E_monitor
*
* %I
* Written by: Kristian Nielsen and Kim Lefmann
* Date: April 20, 1998
* Version: $Revision: 1.25 $
* Origin: Risoe
* Release: McStas 1.11
*
* Energy-sensitive monitor.
*
* %D
* A square single monitor that measures the energy of the incoming neutrons.
*
* Example: E_monitor(xmin=-0.1, xmax=0.1, ymin=-0.1, ymax=0.1,
*                 Emin=1, Emax=50, nchan=20, filename="Output.nrj")
*
* %P
* INPUT PARAMETERS:
*
* xmin:     Lower x bound of detector opening (m)
* xmax:     Upper x bound of detector opening (m)
* ymin:     Lower y bound of detector opening (m)
* ymax:     Upper y bound of detector opening (m)
* xwidth:   Width/diameter of detector (x). Overrides xmin,xmax. (m)
* yheight:  Height of detector (y). Overrides ymin,ymax. (m)
* Emin:     Minimum energy to detect (meV)
* Emax:     Maximum energy to detect (meV)
* nchan:    Number of energy channels (1)
* filename: Name of file in which to store the detector image (text)
*
* OUTPUT PARAMETERS:
*
* E_N:      Array of neutron counts
* E_p:      Array of neutron weight counts
* E_p2:     Array of second moments
* S_p:      Sum of neutron weight counts
* S_pE:     Sum of weighted energies
* S_pE2:    Sum of weighted energy squared
*
* %E
*******************************************************************************/

DEFINE COMPONENT E_monitor
DEFINITION PARAMETERS ()
SETTING PARAMETERS (int nchan=20, char *filename="IE.dat", xmin=0, xmax=0, ymin=0, ymax=0, xwidth=0.2, yheight=0.2, Emin=10, Emax=100)
OUTPUT PARAMETERS (E_N, E_p, E_p2, S_p, S_pE, S_pE2)
STATE PARAMETERS (x,y,z,vx,vy,vz,t,s1,s2,p)
DECLARE
  %{
    double *E_N;
    double *E_p, *E_p2;
    double S_p, S_pE, S_pE2;
  %}
INITIALIZE
  %{
    int i;

    if (nchan <= 0) {
      printf("E_monitor: number of channel must be positive.\n" );
      exit(0);
    }
    E_N = new double[ nchan ];
    E_p = new double[ nchan ];
    E_p2 = new double[ nchan ];

    if (xwidth  > 0) { xmax = xwidth/2;  xmin = -xmax; }
    if (yheight > 0) { ymax = yheight/2; ymin = -ymax; }

    if ((xmin >= xmax) || (ymin >= ymax)) {
            printf("E_monitor: %s: Null detection area !\n"
                   "ERROR      (xwidth,yheight,xmin,xmax,ymin,ymax). Exiting",
           NAME_CURRENT_COMP);
      exit(0);
    }

    printf("E_monitor: initialized. E window=%g,%g, x window=%g,%g, y window = %g, %g\n",
	   Emin, Emax, xmin, xmax, ymin, ymax);

    for (i=0; i<nchan; i++)
    {
      E_N[i] = 0;
      E_p[i] = 0;
      E_p2[i] = 0;
    }
    S_p = S_pE = S_pE2 = 0;
  %}
TRACE
  %{
    int i;
    double E;

    //printf("E_monitor: Trace: %g,%g,%g, %g,%g,%g\n", x,y,z,vx,vy,vz);
    PROP_Z0;
    //printf("E_monitor: Propagated to z=0: %g,%g,%g, %g,%g,%g\n", x,y,z,vx,vy,vz);

    //printf("E_monitor: Trace: E window=%g,%g, x window=%g,%g, y window = %g, %g\n",
    //Emin, Emax, xmin, xmax, ymin, ymax);
    if (x>xmin && x<xmax && y>ymin && y<ymax)
    {
      E = VS2E*(vx*vx + vy*vy + vz*vz);
      //printf("E_monitor: E=%g\n", E);
      S_p += p;
      S_pE += p*E;
      S_pE2 += p*E*E;
      
      i = floor((E-Emin)*nchan/(Emax-Emin));
      // printf("E=%g, Emin=%g, Emax=%g, nchan=%d, i=%d\n", E, Emin, Emax, nchan, i);
      if(i >= 0 && i < nchan)
      {
        E_N[i]++;
        E_p[i] += p;
        E_p2[i] += p*p;
        SCATTER;
      }
    }
  %}
SAVE
  %{
    DETECTOR_OUT_1D(
        "Energy monitor",
        "Energy [meV]",
        "Intensity",
        "E", Emin, Emax, nchan,
        &E_N[0],&E_p[0],&E_p2[0],
        filename);
    if (S_p) printf("<E> : %g meV , E-width : %g meV \n",
     S_pE/S_p,sqrt(S_pE2/S_p - S_pE*S_pE/(S_p*S_p)) );

    printf("E_monitor: saved data to file '%s'.\n", filename);
  %}

FINALLY
%{
  delete [] E_N;
  delete [] E_p;
  delete [] E_p2;
  printf("E_monitor: finalized.\n");
%}

MCDISPLAY
%{
  magnify("xy");
  multiline(5, (double)xmin, (double)ymin, 0.0,
               (double)xmax, (double)ymin, 0.0,
               (double)xmax, (double)ymax, 0.0,
               (double)xmin, (double)ymax, 0.0,
               (double)xmin, (double)ymin, 0.0);
%}

END
