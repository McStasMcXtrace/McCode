/*******************************************************************************
*
* McXtrace, X-ray tracing package
*         Copyright 1997-2002, All rights reserved
*         Risoe National Laboratory, Roskilde, Denmark
*         Institut Laue Langevin, Grenoble, France
*
* Component: Filter
*
* %I
* Written by: Erik Knudsen
* Date: Jan 24, 2011
* Version: $Revision: 1.0$
* Origin: Risoe
* Release: McXtrace 0.9
*
* Block of a attenuating material
* 
* %D
* A rectangular block of attenuating material. Attenuation is computed through
* the effective length travelled within the material. No scattering is modelled at present.
*
* %P
* INPUT PARAMETERS
*
* xwidth: Width of block. (m) 
* yheight: Height of block. (m)
* zdepth:  Thickness of block. (m)
* material_datafile: file where the material parameters for the filter may be found. Format is similar to what may be found off the NISP website. [Be.txt] 
*
* %E
*******************************************************************************/
DEFINE COMPONENT Filter
DEFINITION PARAMETERS (string material_datafile="Be.txt")
SETTING PARAMETERS (xwidth,yheight,zdepth) 
OUTPUT PARAMETERS (prms)
/* X-ray parameters: (x,y,z,kx,ky,kz,phi,t,Ex,Ey,Ez,p) */ 

SHARE
%{
  %include "read_table-lib"
  struct mat_prms {
    int Z;
    double At, rho;
    double *E,*mu;
  };
%}

DECLARE
%{
  double xmax,xmin,ymax,ymin;

  struct mat_prms *prms;

%}

INITIALIZE
%{
  int status=0;
 
  if(!xwidth || !yheight){
    fprintf(stderr,"%s: Lens has zero effective area\n",NAME_CURRENT_COMP);
    exit(0);
  }
  xmax=xwidth/2.0;
  xmin=-xmax;
  ymax=yheight/2.0;
  ymin=-ymax;

  t_Table T;
  if ( (status=Table_Read(&T,material_datafile,0))==-1){
    fprintf(stderr,"Error: Could not parse file \"%s\" in COMP %s\n",material_datafile,NAME_CURRENT_COMP);
    exit(-1);
  }
  char **header_parsed;
  header_parsed=Table_ParseHeader(T.header,"Z","A[r]","rho","Z/A","sigma[a]",NULL);
  prms=calloc(1,sizeof(struct mat_prms));
  prms->E=malloc(sizeof(double)*(T.rows+1));
  prms->mu=malloc(sizeof(double)*(T.rows+1));
  if(header_parsed[2]){prms->rho=strtod(header_parsed[2],NULL);}
  else{fprintf(stderr,"Warning(%s): %s found in header of %s, set to 1\n",NAME_CURRENT_COMP,"rho",material_datafile);prms->rho=1;}
  /*which columns holds the mus*/
  int mu_c=5;
  if (T.columns==3) mu_c=1;

  int i;
  for (i=0;i<T.rows;i++){
    prms->E[i]=T.data[i*T.columns];
    prms->mu[i]=T.data[mu_c+i*T.columns]*prms->rho*1e2;     /*mu is now in SI, [m^-1]*/ 
  }

  prms->E[T.rows]=-1.0;
  prms->mu[T.rows]=-FLT_MAX;
  
  Table_Free(&T);
%}

TRACE
%{
  double alpha,e,k,mu;
  double l0,l1;
  int i;
  if (box_intersect(&l0,&l1,x,y,z,kx,ky,kz,xwidth,yheight,zdepth)){
    PROP_DL(l0);
    /*table interpolation*/
    k=sqrt(kx*kx+ky*ky+kz*kz);
    e=k*K2E;
    i=0;
    while (e>prms->E[i]){
      i++;
      if (prms->E[i]==-1){
        fprintf(stderr,"Photon energy (%g keV) is outside the filter's material data\n",e); ABSORB;
      }
    }
    alpha=(e-prms->E[i-1])/(prms->E[i]-prms->E[i-1]);
    mu=(1-alpha)*prms->mu[i-1]+alpha*prms->mu[i];
    //mu= 1e-10*mu;  /*factor conversion from m^-1 to A^-1*/
    
    l1-=l0; 
    p*=exp(-mu*l1);

    PROP_DL(l1);
  }
%}


MCDISPLAY
%{
  magnify("xy");
  box(0,0,0,xwidth,yheight,zdepth);
%}
END
