/*******************************************************************************
*         McStas instrument definitio URL=http://www.mcstas.org
*
* Instrument: SEMSANS
*
* %Identification
* Written by: Morten Sales
* Date: 2014
* Origin: Copenhagen, Berlin, Delft
* Release: McStas 2.0
* Version: $Revision: ??
* INSTRUMENT_SITE: TU Delft. 
*
* SEMSANS-instrument
* 
* %Description
* SEMSANS instrument with 2 isosceles triangular field coils
*******************************************************************************/

DEFINE INSTRUMENT Delft_SEMSANS(
								triacoil_depth  = 1.935000e-01, //Half of the triangular coil depth in z-direction (one right triangle) [m]
								triacoil_width  = 7.042824e-02, //xwidth of triangular coil [m]
								triacoil_height = 3.000000e-01, //yheight of triangular coil  [m]
								pol_zdepth      = 9.300000e-01, //Depth of the polariser along z [m]
								vcoil_zdepth    = 1.500000e-01, //Depth along z of the v-coil flipper [m]
								Guide1_depth    = 1.650000e-02, //Depth of first guide/precession field section [m]
								Guide2_depth    = 3.315000e-01, //Depth of second guide/precession field section [m]
								Guide3_depth    = 3.315000e-01, //Depth of third guide/precession field section [m]
								Guide4_depth    = 5.650000e-02, //Depth of fourth guide/precession field section [m]
								Bguide          = -5.000000e-04, //Field strength of guide/precession field [T]
								Bextra			= -2.120000e-02, //Field strenth of extra field in guide field 1 [T] to acheive echo
								Bt2             = -4.440000e-03, //Field in first triangular coil [T]
								Bt1             = -2.560000e-03, //Field in second triangular coil [T]
								DLambda         = 4.166366e+00, //Wavelength spread from source (half of it) [Å]
								Lambda          = 4.559500e+00, //Mean wavelength [Å]
								flippos         = 3.100000e-02, //Position (away from position set by vcoil_34_pos) of pi-flipper [m]
								FLIP            = 1.000000e+00, //Choose polarisation direction using v-coil pi/2-flipper (1 is down, -1 is up)
								chop1_pos       = 5.000000e-01, //Position of first chopper [m]
								chop2_pos       = 9.740000e-01, //Position of second chopper [m]
								pol_pos         = 1.907000e+00, //Position of polariser [m]
								analyser_pos    = 6.072000e+00, //Position of analyser [m]
								grating_w       = 1.570000e-03, //Width of transmitting part of grating [m]
								grating_a       = 2.930000e-03, //Width of absorbing part of grating [m]
								slit_1_pos      = 2.957000e+00, //Position of first slit [m]
								slit_2_pos      = 5.437000e+00, //Position of second slit [m]
								Guide1_pos      = 3.307000e+00, //Position of first guide/precession field [m]
								Guide2_pos      = 3.710500e+00, //Position of second guide/precession field [m]
								Guide3_pos      = 4.342000e+00, //Position of third guide/precession field [m]
								Guide4_pos      = 5.060500e+00, //Position of fourth guide/precession field [m]
								vcoil_12_pos    = 3.157000e+00, //Position of first v-coil pair [m]
								vcoil_34_pos    = 4.192000e+00, //Position of second v-coil pair [m]
								vcoil_56_pos    = 5.267000e+00, //Position of third v-coil pair [m]
								triacoil_1_pos  = 3.517000e+00, //Position of first triangular coil (centre) [m]
								triacoil_2_pos  = 4.867000e+00, //Position of second triangular coil (centre) [m]
								grating_pos     = 6.757000e+00, //Position of grating [m]
								detector_pos    = 6.957000e+00, //Position of detector [m]
								tlow            = 2.190523040779461e+03, //tmin of detector [mu-s]
							    thigh			= 1.214744595341338e+04) //tmax of detector [mu-s]

/* The DECLARE section allows us to declare variables or  small      */
/* functions in C syntax. These may be used in the whole instrument. */
DECLARE
%{

%}

/* The INITIALIZE section is executed when the simulation starts     */
/* (C code). You may use them as component parameter values.         */
INITIALIZE
%{

%}
/* Here comes the TRACE section, where the actual      */
/* instrument is defined as a sequence of components.  */
TRACE

COMPONENT Origin = Progress_bar()
AT(0,0,0) ABSOLUTE

// Source
COMPONENT source = Source_simple(
  radius=0, dist = slit_1_pos, focus_xw=20e-3, focus_yh=20e-3,
  xwidth = 2e-3, yheight = 2e-3,
  lambda0 = Lambda,dlambda = DLambda)
AT (0, 0, 0) RELATIVE Origin

// First chopper
COMPONENT chop1 = DiskChopper(radius=0.21, theta_0=20, nu=25, nslit=2, phase=-10, isfirst=1, yheight=0.21)
AT (0, 0, chop1_pos) RELATIVE Origin

// Second chopper
COMPONENT chop2 = DiskChopper(radius=0.21, theta_0=20, nu=25, nslit=2, phase=20-10, isfirst=0, yheight=0.21)
AT (0, 0, chop2_pos) RELATIVE Origin

// Polariser
COMPONENT polarizer = Set_pol(py=1)
AT (0, 0, pol_pos+0.5*pol_zdepth) RELATIVE Origin

//First Slit
COMPONENT slit_1 = Slit(
   xwidth=15e-3,yheight=15e-3)
AT (0,0,slit_1_pos) RELATIVE Origin

//First v-coil of first v-coil pair
COMPONENT vcoil1=Pol_pi_2_rotator(
    xwidth=0.15,yheight=0.15,zdepth=vcoil_zdepth,rx=0,ry=0,rz=FLIP*1)
AT (0,0,vcoil_12_pos-vcoil_zdepth) RELATIVE Origin

//Second v-coil of first v-coil pair
COMPONENT vcoil2=Pol_pi_2_rotator(
    xwidth=0.15,yheight=0.15,zdepth=vcoil_zdepth,rx=0,ry=1,rz=0)
AT (0,0,vcoil_12_pos) RELATIVE Origin


// START GUIDEFIELD 1
COMPONENT Guide_field1 = Pol_simpleBfield(xwidth=0.4, yheight=0.4, zdepth=Guide1_depth, Bx=0, By=Bguide+Bextra, Bz=0,fieldFunction=const_magnetic_field)
     AT (0,0,Guide1_pos) RELATIVE Origin


// STOP GUIDE FIELD 1
COMPONENT Guide_field1_cp = Pol_simpleBfield_stop(magnet_comp_stop=Guide_field1)
     AT (0,0,Guide1_pos+Guide1_depth) RELATIVE Origin


// TRIA COIL 1
COMPONENT triacoil_1=Pol_triafield(
    xwidth=triacoil_width,yheight=triacoil_height,zdepth=2*triacoil_depth, 
    B=Bt1, Bguide=Bguide)
  AT (0,0,triacoil_1_pos-triacoil_depth) RELATIVE Origin


// START GUIDEFIELD 2
COMPONENT Guide_field2 = Pol_simpleBfield(xwidth=0.4, yheight=0.4, zdepth=Guide2_depth+flippos, Bx=0, By=Bguide, Bz=0,fieldFunction=const_magnetic_field)
     AT (0,0,Guide2_pos) RELATIVE Origin


// STOP GUIDE FIELD 2
COMPONENT Guide_field2_cp = Pol_simpleBfield_stop(magnet_comp_stop=Guide_field2)
  AT (0,0,Guide2_pos+Guide2_depth+flippos) RELATIVE Origin


//First v-coil of second v-coil pair
COMPONENT vcoil3=Pol_pi_2_rotator(
    xwidth=0.15,yheight=0.15,zdepth=vcoil_zdepth,rx=0,ry=0,rz=1)
AT (0,0,vcoil_34_pos-vcoil_zdepth+flippos) RELATIVE Origin

//Second v-coil of second v-coil pair
COMPONENT vcoil4=Pol_pi_2_rotator(
    xwidth=0.15,yheight=0.15,zdepth=vcoil_zdepth,rx=0,ry=0,rz=1)
AT (0,0,vcoil_34_pos+flippos) RELATIVE Origin


// START GUIDEFIELD 3
COMPONENT Guide_field3 = Pol_simpleBfield(xwidth=0.4, yheight=0.4, zdepth=Guide3_depth-flippos, Bx=0, By=Bguide, Bz=0,fieldFunction=const_magnetic_field)
     AT (0,0,Guide3_pos+flippos) RELATIVE Origin


// STOP GUIDE FIELD 3
COMPONENT Guide_field3_cp = Pol_simpleBfield_stop(magnet_comp_stop=Guide_field3)
     AT (0,0,Guide3_pos+Guide3_depth) RELATIVE Origin


// START TRIA COIL 2
COMPONENT triacoil_2=Pol_triafield(
    xwidth=triacoil_width,yheight=triacoil_height,zdepth=2*triacoil_depth, 
    B=Bt2, Bguide=Bguide)
  AT (0,0,triacoil_2_pos-triacoil_depth) RELATIVE Origin


// START GUIDEFIELD 4
COMPONENT Guide_field4 = Pol_simpleBfield(xwidth=0.4, yheight=0.4, zdepth=Guide4_depth, Bx=0, By=Bguide, Bz=0,fieldFunction=const_magnetic_field)
     AT (0,0,Guide4_pos) RELATIVE Origin


// STOP GUIDE FIELD 4
COMPONENT Guide_field4_cp = Pol_simpleBfield_stop(magnet_comp_stop=Guide_field4)
  AT (0,0,Guide4_pos+Guide4_depth) RELATIVE Origin

//First v-coil of third v-coil pair
COMPONENT vcoil5=Pol_pi_2_rotator(
    xwidth=0.15,yheight=0.15,zdepth=vcoil_zdepth,rx=0,ry=1,rz=0)
AT (0,0,vcoil_56_pos-vcoil_zdepth) RELATIVE Origin

//Second v-coil of third v-coil pair
COMPONENT vcoil6=Pol_pi_2_rotator(
    xwidth=0.15,yheight=0.15,zdepth=vcoil_zdepth,rx=0,ry=0,rz=1)
AT (0,0,vcoil_56_pos) RELATIVE Origin

//Second slit
COMPONENT slit_2=Slit(
    xwidth=15e-3,yheight=15e-3)
AT(0,0,slit_2_pos) RELATIVE Origin

//Analyser
COMPONENT analyser=Analyser_ideal(mx=0,my=1,mz=0)
     AT(0,0,analyser_pos) RELATIVE Origin


//THIS IS GRATING
		COMPONENT GratingSlit1_1 = Slit(xwidth=grating_w,yheight=5e-3)
			AT (-2.0*grating_w - 2.0*grating_a, 0, grating_pos) RELATIVE Origin
			 GROUP Grating
    
		COMPONENT GratingSlit1_2 = Slit(xwidth=grating_w,yheight=5e-3)
			AT (-1.0*grating_w - 1.0*grating_a, 0, grating_pos) RELATIVE Origin
 		    GROUP Grating

		COMPONENT GratingSlit1_3 = Slit(xwidth=grating_w,yheight=5e-3)
			AT (0, 0, grating_pos) RELATIVE Origin
			 GROUP Grating
    
		COMPONENT GratingSlit1_4 = Slit(xwidth=grating_w,yheight=5e-3)
			AT (1.0*grating_w + 1.0*grating_a, 0, grating_pos) RELATIVE Origin
 		    GROUP Grating

		COMPONENT GratingSlit1_5 = Slit(xwidth=grating_w,yheight=5e-3)
			AT (2.0*grating_w + 2.0*grating_a, 0, grating_pos) RELATIVE Origin
 		    GROUP Grating
/*
//THIS IS DOUBLE SLIT
		COMPONENT GratingSlit2_1 = Slit(xwidth=grating_w,yheight=5e-3)
			AT (-0.5*grating_w - 0.5*grating_a, 0, grating_pos) RELATIVE Origin
			 GROUP Grating
    
		COMPONENT GratingSlit2_2 = Slit(xwidth=grating_w,yheight=5e-3)
			AT (0.5*grating_w + 0.5*grating_a, 0, grating_pos) RELATIVE Origin
			 GROUP Grating
*/
/*
//THIS IS SINGLE SLIT
		COMPONENT GratingSlit3_1 = Slit(xwidth=grating_w,yheight=5e-3)
			AT (0, 0, grating_pos) RELATIVE Origin
*/


COMPONENT TOF_det =  TOF_monitor(xwidth = 0.05, yheight = 0.05,
          					     nt=251, tmin=tlow, tmax=thigh, filename="TOF_det")
AT (0, 0, detector_pos) RELATIVE Origin


/* This section is executed when the simulation ends (C code). Other    */
/* optional sections are : SAVE                                         */
FINALLY
%{
%}

/* The END token marks the instrument definition end */
END



