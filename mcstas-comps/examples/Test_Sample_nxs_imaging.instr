/************************************************
*
* Test TOF-imaging instrument for the Sample_nxs.comp component
*
* %Identification
* Written by: Mirko Boin (HZB)
* Date: 2012
* Origin: HZB
* %INSTRUMENT_SITE: HZB
*
* Test TOF-imaging instrument for the Sample_nxs.comp component.
* %Description
*
* This instrument relies on the 'nxs' library that is provided with McStas as of release 2.0.
* Please see in your MCSTAS/lib/share/nxslib/README.TXT and the links in the section below for
* further instructions and more details.
*
* Example: Test_Sample_nxs_imaging.instr
*
* %Parameters
* lambda1: [AA]                    Lower bound of wavelength band
* lambda2: [AA]                    Upper bound of wavelength band
* sample_detector_distance: [m]    Distance between sample and detector
* moderator_pinhole_distance: [m]  Distance between moderator and pinhole
* pinhole_sample_distance: [m]     Distance between pinhole and sample
* sample_x: [m]                    Sample translation along x
* thickness: [m]                   Sample thickness along z
* sample_file: [str]               Crystallographic structure from 'nsx' file
* moderator_face: [str]            Chosen ISIS moderator
*
* %Link
* M. Boin (2012), <i>J. Appl. Cryst.</i> <b>45</b>, 603-607, <a href="http://dx.doi.org/10.1107/S0021889812016056">doi:10.1107/S0021889812016056</a>
* M. Boin, R.C. Wimpory, A. Hilger, N. Kardjilov, S.Y. Zhang, M. Strobl (2012), <i>J. Phys.: Conf. Ser.</i> <b>340</b>, 012022, <a href="http://dx.doi.org/10.1088/1742-6596/340/1/012022">doi:10.1088/1742-6596/340/1/012022</a>
* M. Boin, A. Hilger, N. Kardjilov, S.Y. Zhang, E.C. Oliver, J.A. James, C. Randau, R.C. Wimpory (2011), <i>J. Appl. Cryst.</i> <b>44</b>, 1040-1046, <a href="http://dx.doi.org/10.1107/S0021889811025970">doi:10.1107/S0021889811025970</a>
*
* %End
***************************************************/
DEFINE INSTRUMENT Test_Sample_nxs_imaging( lambda1=3.7, lambda2=4.4, sample_detector_distance=0.05, moderator_pinhole_distance=30.0, pinhole_sample_distance=0.05, sample_x = 0.0, thickness = 0.02, string sample_file="Cu.nxs", string moderator_face="water" )

DECLARE
%{
%}

INITIALIZE
%{

%}
TRACE

COMPONENT a1 = Arm()
  AT (0,0,0) ABSOLUTE


COMPONENT moderator = ISIS_moderator(
  Face = moderator_face, Emin = -lambda1, Emax = -lambda2, dist = 4.0,
  focus_xw = 0.001, focus_yh = 0.001, xwidth = 0.001, yheight = 0.001,
  CAngle = 0.0, SAC = 1)
 AT (0.0, 0.0, 0.0) RELATIVE a1


COMPONENT pinhole = Slit(
  xwidth = 0.002,
  yheight = 0.002
) AT (0.0, 0.0, moderator_pinhole_distance) RELATIVE moderator


COMPONENT sample1 = Sample_nxs(
  xwidth = 0.005,
  yheight = 0.005,
  zthick = thickness,
  nxsFileName = sample_file,
  max_hkl=8,
  TransOnly = 0,
  IncohScat = 1,
  MultiScat = 1,
  d_phi = 110.0
) AT (sample_x, 0.0, pinhole_sample_distance) RELATIVE pinhole ROTATED (0,0,0) ABSOLUTE

COMPONENT sample = Arm()
  AT (0,0,0) RELATIVE sample1 ROTATED (0,0,0) RELATIVE sample1


COMPONENT tofmon = TOF_monitor(
  nt = 1000,
  filename = "tofmon.dat",
  xwidth =0.01,
  yheight = 0.01,
  tmin = 15000,
  tmax = 45000,
  restore_neutron = 1
) AT (-sample_x, 0, sample_detector_distance) RELATIVE sample
COMPONENT lmon = L_monitor(
  nL = 50,
  filename = "lmon.dat",
  xwidth = 0.01,
  yheight = 0.01,
  Lmin = lambda1,
  Lmax = lambda2,
  restore_neutron = 0
) AT (-sample_x, 0, sample_detector_distance) RELATIVE sample

FINALLY
%{
%}

END
