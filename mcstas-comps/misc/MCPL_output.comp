/*******************************************************************************
*
* McStas, neutron ray-tracing package
*         Copyright (C) 1997-2007, All rights reserved
*         Risoe National Laboratory, Roskilde, Denmark
*         Institut Laue Langevin, Grenoble, France

* Component: MCPL_output
*
* %I
* Written by: Erik B Knudsen 
* Date: Mar 2016
* Version: $Revision$
* Origin: DTU Physics
* Release: McStas 2.3
*
* Detector-like component that writes neutron state parameters into an mcpl-format 
* binary, virtual-source neutron file.
*
* %D
* Detector-like component that writes neutron state parameters into an mcpl-format 
* binary, virtual-source neutron file.
*
* MCPL is short for Monte Carlo Particle List, and is a new format for sharing events
* between e.g. MCNP(X), Geant4 and McStas.
*
* When used with MPI, the component will output #MPI nodes individual MCPL files that
* can be merged using the mcpltool.
* %BUGS
*
* %P
* INPUT PARAMETERS
*
* filename: [str] Name of neutron file to write. Default is standard
*                 output [string]. If not given, a unique name will be used.
* bufsize:  [1]   Size of neutron output buffer
*                 default is 0, i.e. save all - recommended.
* verbose:  [ ]   Print debugging information for first 10 particles stored.
* dump_file: [ ]  
* polarisationuse: [ ]  Enable storing the polarisation state of the neutron.
* doubleprec: [ ] Use double precision storage
*
* %E
*******************************************************************************/

DEFINE COMPONENT MCPL_output
DEFINITION PARAMETERS (polarisationuse=1, doubleprec=1, dump_file=0, verbose=0)
SETTING PARAMETERS (string filename=0)
OUTPUT PARAMETERS (outputfile)
DEPENDENCY "-I@MCCODE_LIB@/libs/mcpl -L@MCCODE_LIB@/libs/mcpl -lmcpl"

SHARE
%{
#include <mcpl.h>
%}

DECLARE
%{
    mcpl_outfile_t outputfile;
%}

INITIALIZE
%{
#if defined (USE_MPI)
  /* In case of MPI, simply redefine the filename used by each node */
  MPI_MASTER(fprintf(stdout, "\n\nWARNING: You are using MCPL_output with MPI, hence your will get %i filenames %s_node_#i as output.\n\n",mpi_node_count,filename); );
  sprintf(filename,"%s.node_%i",filename,mpi_node_rank);
#endif

    char line[256];
    outputfile = mcpl_create_outfile(filename);
    snprintf(line,255,"%s %s %s",MCCODE_NAME,MCCODE_VERSION,mcinstrument_name);
    mcpl_hdr_set_srcname(outputfile,line);
    mcpl_enable_universal_pdgcode(outputfile,2112);/*all particles are neutrons*/
    snprintf(line,255,"Output by COMPONENT: %s",NAME_CURRENT_COMP);
    mcpl_hdr_add_comment(outputfile,line);

    if (polarisationuse) {
        mcpl_enable_polarisation(outputfile);
    }
    if (doubleprec){
        mcpl_enable_doubleprec(outputfile);
    }

#if defined (USE_MPI)   
  MPI_MASTER(
#endif

    if (verbose==1) {
      printf("MCPL_output verbose mode - outputting data on the 10 first stored neutrons in MCPL units:\n");
    }

#if defined (USE_MPI)   
	    );
#endif

  /*Add comments on what the orientation and position of this component is.*/
  /*Include the instrument file itself as a binary blob in the mcpl file*/

  /*Have the option of including a user-flag like they do at Loki.*/

%}

TRACE
%{
    double nrm;
    mcpl_particle_t *particle = (mcpl_particle_t *) calloc(sizeof(mcpl_particle_t),1);
    /*positions are in cm*/
    particle->position[0]=x*100;
    particle->position[1]=y*100;
    particle->position[2]=z*100;

    if(polarisationuse){
        particle->polarisation[0]=sx;
        particle->polarisation[1]=sy;
        particle->polarisation[2]=sz;
    }

    nrm =sqrt(vx*vx + vy*vy + vz*vz);
    /*ekin is in MeV*/
    particle->ekin = VS2E*nrm*nrm/1e9; 
    particle->direction[0] = vx/nrm;
    particle->direction[1] = vy/nrm;
    particle->direction[2] = vz/nrm;
    /*time in ms:*/
    particle->time = t*1e3;
    /*weight in unspecified units:*/
    particle->weight = p;

#if defined (USE_MPI)   
  MPI_MASTER(
#endif

    if (verbose==1 && mcrun_num<10) {
      printf("id=%ld\tpdg=2112\tekin=%g MeV\tx=%g cm\ty=%g cm\tz=%g cm\tux=%g\tuy=%g\tuz=%g\tt=%g ms\tweight=%g\tpolx=%g\tpoly=%g\tpolz=%g\n",
	     mcrun_num, particle->ekin, particle->position[0], particle->position[1], particle->position[2],
	     particle->direction[0], particle->direction[1], particle->direction[2], particle->time, particle->weight,
	     particle->polarisation[0], particle->polarisation[1], particle->polarisation[2]);
    }

#if defined (USE_MPI)   
	    );
#endif


    mcpl_add_particle(outputfile,particle);
    
    SCATTER;
%}

SAVE
%{
    mcpl_closeandgzip_outfile(outputfile);  
%}

FINALLY
%{
    if (dump_file) mcpl_dump(filename,0,0,(int) dump_file);
%}

MCDISPLAY
%{
    double t,dt;
    int i;
    multiline(5, 0.2,0.2,0.0, -0.2,0.2,0.0, -0.2,-0.2,0.0, 0.2,-0.2,0.0, 0.2,0.2,0.0);
    /*M*/
    multiline(5,-0.085,-0.085,0.0, -0.085,0.085,0.0, -0.045,-0.085,0.0, -0.005,0.085,0.0, -0.005,-0.085,0.0);
    /*O*/
    dt=2*M_PI/32;
    t=0;
    for (i=0;i<32;i++){
        line(0.04*cos(t)+0.045,0.08*sin(t),0, 0.04*cos(t+dt)+0.045,0.08*sin(t+dt),0);
        t+=dt;
    }
%}

END

