/*******************************************************************************
*
* McStas, neutron ray-tracing package
*         Copyright 1997-2002, All rights reserved
*         Risoe National Laboratory, Roskilde, Denmark
*         Institut Laue Langevin, Grenoble, France
*
* Component: Sqq_w_monitor
*
* %I
* Written by: Peter Willendrup
* Date: June-July, 2018
* Origin: DTU
*
* Monitor outputting a series of energy-planes in a subset of reciprocal space, spanned by 
* scattering vectors qa(x,z) and qb(x,z) in the component x-z plane.
*
* %D
*
* Cylindrical monitor on the x-z plane outputting a series of energy-planes in a subset of reciprocal 
* space, spanned by scattering vectors qa(x,z) and qb(x,z).
*
* The radius and yheight parameters are not used for propagation, but only to define the outgoing divergence 
* limit considered when estimating k_f
*
* The assumption is that the "current" neutron represents the final state, whereas the incoming state
* is found by restoring the neutron state "index" components earlier.
*
* Example: Sqq_w_monitor(filename="output",radius=1, yheight=0.05, Emin=0,Emax=5,nE=11,nqa=100,nqb=100,qamin=1,qamax=10,qbmin=1qbmax=10,index=-2)
*          AT (0,0,0) RELATIVE sample
*
* %P
* INPUT PARAMETERS:
*
* radius:  [m]       Cylinder radius
* yheight: [m]       Cylinder height  
* qax:     [1]       x-component of 1st q-vector
* qaz:     [1]       z-component of 1st q-vector
* qbx:     [1]       x-component of 2nd q-vector
* qbz:     [1]       z-component of 2nd q-vector
* qamin:   [AA^-1]   Defines interval (qamin,qamax) where monitor measures in nqa bins
* qamax:   [AA^-1]   Defines interval (qamin,qamax) where monitor measures in nqa bins
* qbmin:   [AA^-1]   Defines interval (qbmin,qbmax) where monitor measures in nqb bins
* qbmax:   [AA^-1]   Defines interval (qbmin,qbmax) where monitor measures in nqb bins
* nqa:     [int]     Number of bins along qa direction
* nqb:     [int]     Number of bins along qb direction
* Emin:    [meV]     Defines the energy-transfer [Emin,Emax] window to monitor in nE bins
* Emax:    [meV]     Defines the energy-transfer [Emax,Emax] window to monitor in nE bins
* nE:    [int]       Number of energy slices
* filename: [string] Base filename to use, nE+1 files will be output
* nowritefile: [1]   If set, monitor will skip writing to disk
* nosum: [1]         If set, monitor will skip writing the energy-summed array to disk
*
* OUTPUT PARAMETERS:
*
* M_N: []             3D array of neutron counts
* M_p: []             3D array of neutron weight counts
* N_p2: []            3D array of second moments
* M_Ns: []            2D array of neutron counts
* M_ps: []            2D array of neutron weight counts
* N_p2s: []           2D array of second moments
*
* %E
*******************************************************************************/


DEFINE COMPONENT Sqq_w_monitor
DEFINITION PARAMETERS (radius=1, yheight=0.05, qax=1,qaz=0,qbx=0,qbz=1,qamin=0,qamax=2,qbmin=0,qbmax=2,Emin=0,Emax=5,nqa=90, nqb=90, nE=10, string filename, index=-2, int nowritefile=0, int nosum=0)
SETTING PARAMETERS ()
OUTPUT PARAMETERS (M_N, M_p, M_p2, M_Ns, M_ps, M_p2s)

DECLARE
%{
double M_N[nE][nqa][nqb];
double M_p[nE][nqa][nqb];
double M_p2[nE][nqa][nqb];
double M_Ns[nqa][nqb];
double M_ps[nqa][nqb];
double M_p2s[nqa][nqb];
double dE;
%}

INITIALIZE
%{
int i,j,k;

/* Make checks for limits on qa, qb, w grid */

    for (i=0; i<nqa; i++)
      for (j=0; j<nqb; j++) {
	M_Ns[i][j] = 0;
	M_ps[i][j] = 0;
	M_p2s[i][j] = 0;
	for (k=0; k<nE; k++) {
	  M_N[k][i][j] = 0;
	  M_p[k][i][j] = 0;
	  M_p2[k][i][j] = 0;
	}
      }

    dE=(Emax-Emin)/(1.0*nE-1);
%}
TRACE
  %{
    int i,j,k;
    double rx,ry,rz,rvx,rvy,rvz,rt,rsx,rsy,rsz,rp;
    double Ei,Ef,E,Ki,Kf;
    double qx,qy,qz;
    double qqa, qqb;
    double kix,kiy,kiz;
    double kfx,kfy,kfz;
    double t0,t1;
    

    double nx,ny,nz;
    nx=vx;
    ny=vy;
    nz=vz;
    NORM(nx,ny,nz);
    double v_div=scalar_prod(nx, ny, nz, 0, 1, 0);
    /* Initial check to see if this neutron should be counted or not... */ 
    if (fabs(v_div)<=yheight/radius && cylinder_intersect(&t0, &t1, x, y, z, vx, vy, vz, radius, yheight)) {
      if(t0<0 && t1>0) {	 
	/* This one hits our cylindrical band, arriving from inside */

	RESTORE_NEUTRON(INDEX_CURRENT_COMP+index, rx,ry,rz,rvx,rvy,rvz,rt,rsx,rsy,rsz,rp);
	/* Calculate energy transfer */
	Ei = VS2E*(rvx*rvx + rvy*rvy + rvz*rvz);
	Ef = VS2E*(  vx*vx +  vy*vy +    vz*vz);
	E=Ei-Ef;
	
	/* calculate k vectors and momentum transfer*/
	kix=rvx;
	kiy=rvy;
	kiz=rvz;
	kfx=vx;
	kfy=vy;
	kfz=vz;
	NORM(kix, kiy, kiz);
	NORM(kfx, kfy, kfz);
	
	/* K-vector lengths */
	Ki=V2K*sqrt((rvx*rvx)+(rvy*rvy)+(rvz*rvz));
	Kf=V2K*sqrt((vx*vx)+(vy*vy)+(vz*vz));
	kix=Ki*kix; kiy=Ki*kiy; kiz=Ki*kiz; 
	kfx=Kf*kfx; kfy=Kf*kfy; kfz=Kf*kfz; 
	
	qx=kix-kfx;
	qy=kiy-kfy;
	qz=kiz-kfz;
	
	/* Calculate projections along qa and qb */
	qqa = qx*qax + qz*qaz;
	qqb = qx*qbx + qz*qbz;
	
	/* Check if we are within the selected q/e range */
	if (qqa <= qamax && qqa >=qamin && qqb <=qbmax && qqb>=qbmin && E<= Emax && E>=Emin) {
	  
	  i = floor((qqa - qamin)*nqa/(qamax - qamin));
	  j = floor((qqb - qbmin)*nqb/(qbmax - qbmin));
	  k = floor((E - Emin)*nE/(Emax - Emin));
	  
	  M_N[k][i][j]++;
	  M_p[k][i][j] += p;
	  M_p2[k][i][j] += p*p;
	  
	  M_Ns[i][j]++;
	  M_ps[i][j] += p;
	  M_p2s[i][j] += p*p;
	  
	  SCATTER;
	}
      } 
    } 
    /* Stuff that don't hit cylinder properly is disregarded */
%}

SAVE
%{
    if (!nowritefile) {
      
  int kk,ll;
  char ff[256];
  char tt[256];
  if (!nosum) {
  sprintf(ff, "%s_Sum",filename);
  DETECTOR_OUT_2D(
	"qa vs qb monitor E Sum",
        "qa [AA^-1]",
        "qb [AA^-1]",
        qamin, qamax, qbmin, qbmax,
        nqa, nqb,
        &M_Ns[0][0],&M_ps[0][0],&M_p2s[0][0],
        ff);
  }
  for (kk=0; kk<nE; kk++) {
    sprintf(ff, "%s_%i",filename,kk);
    sprintf(tt, "qa vs qb monitor E slice %i ~ %g meV",kk,dE*kk+Emin);
    DETECTOR_OUT_2D(
	tt,
        "qa [AA^-1]",
        "qb [AA^-1]",
	qamin, qamax, qbmin, qbmax,
        nqa, nqb,
        &M_N[kk][0][0],&M_p[kk][0][0],&M_p2[kk][0][0],
        ff);
  }
    }
%}

MCDISPLAY
%{
  circle("xz", 0, -(yheight/2.0), 0, radius );
  circle("xz", 0,  (yheight/2.0), 0, radius );
%}

END
