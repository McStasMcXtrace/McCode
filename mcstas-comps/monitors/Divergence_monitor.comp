/*******************************************************************************
*
* McStas, neutron ray-tracing package
*         Copyright 1997-2002, All rights reserved
*         Risoe National Laboratory, Roskilde, Denmark
*         Institut Laue Langevin, Grenoble, France
*
* Component: Divergence_monitor
*
* %I
* Written by: Kim Lefmann
* Date: Nov. 11, 1998
* Origin: Risoe
*
* Horizontal+vertical divergence monitor.
*
* %D
* A divergence sensitive monitor. The counts are distributed in
* (n times m) pixels.
*
* Example: Divergence_monitor(nh=20, nv=20, filename="Output.pos",
*           xmin=-0.1, xmax=0.1, ymin=-0.1, ymax=0.1,
*           maxdiv_h=2, maxdiv_v=2)
*
* %P
* INPUT PARAMETERS:
*
* xmin: [m]             Lower x bound of detector opening
* xmax: [m]             Upper x bound of detector opening
* ymin: [m]             Lower y bound of detector opening
* ymax: [m]             Upper y bound of detector opening
* xwidth: [m]           Width of detector. Overrides xmin, xmax
* yheight: [m]          Height of detector. Overrides ymin, ymax
* nv: [1]               Number of pixel columns
* nh: [1]               Number of pixel rows
* nx: [1]
* ny: [1]               Vector definition of "forward" direction wrt. divergence, to be used e.g. when the monitor is rotated into the horizontal plane
* nz: [1]
* maxdiv_v: [degrees]   Maximal vertical divergence detected
* maxdiv_h: [degrees]   Maximal vertical divergence detected
* filename: []          Name of file in which to store the detector image text
* restore_neutron: [1]  If set, the monitor does not influence the neutron state
*
* OUTPUT PARAMETERS:
*
* Div_N: []             Array of neutron counts
* Div_p: []             Array of neutron weight counts
* Div_p2: []            Array of second moments
*
* %E
*******************************************************************************/
DEFINE COMPONENT Divergence_monitor

DEFINITION PARAMETERS ()

SETTING PARAMETERS (nh=20, nv=20,
  string filename=0, xmin=-0.05, xmax=0.05, ymin=-0.05, ymax=0.05,
  xwidth=0, yheight=0, maxdiv_h=2, maxdiv_v=2, restore_neutron=0, nx=0, ny=0, nz=1)

OUTPUT PARAMETERS ()
/* Neutron parameters: (x,y,z,vx,vy,vz,t,sx,sy,sz,p) */

DECLARE
%{
  DArray2d Div_N;
  DArray2d Div_p;
  DArray2d Div_p2;
%}

INITIALIZE
%{
  int i,j;

  if (xwidth  > 0) { xmax = xwidth/2;  xmin = -xmax; }
  if (yheight > 0) { ymax = yheight/2; ymin = -ymax; }

  if ((xmin >= xmax) || (ymin >= ymax)) {
    printf("Divergence_monitor: %s: Null detection area !\n"
           "ERROR               (xwidth,yheight,xmin,xmax,ymin,ymax). Exiting",
           NAME_CURRENT_COMP);
    exit(0);
  }

  Div_N = create_darr2d(nh, nv);
  Div_p = create_darr2d(nh, nv);
  Div_p2 = create_darr2d(nh, nv);

  for (i=0; i<nh; i++)
    for (j=0; j<nv; j++)
    {
      Div_N[i][j] = 0;
      Div_p[i][j] = 0;
      Div_p2[i][j] = 0;
    }
  NORM(nx,ny,nz);
%}

TRACE
%{
  int i,j;
  double h_div, v_div;
  double v, vn;

  PROP_Z0;
  if (x>xmin && x<xmax && y>ymin && y<ymax)
  {
    /* Find length of projection onto the [nx ny nz] axis */
    vn = scalar_prod(vx, vy, vz, nx, ny, nz);
    h_div = RAD2DEG*atan2(vx,vn);
    v_div = RAD2DEG*atan2(vy,vn);
    if (h_div < maxdiv_h && h_div > -maxdiv_h &&
        v_div < maxdiv_v && v_div > -maxdiv_v)
    {
      i = floor((h_div + maxdiv_h)*nh/(2.0*maxdiv_h));
      j = floor((v_div + maxdiv_v)*nv/(2.0*maxdiv_v));
      double p2 = p*p;
      #pragma acc atomic
      Div_N[i][j] = Div_N[i][j] + 1;
      #pragma acc atomic
      Div_p[i][j] = Div_p[i][j] + p;
      #pragma acc atomic
      Div_p2[i][j] = Div_p2[i][j] + p2;
      SCATTER;
    }
  }
  if (restore_neutron) {
    RESTORE_NEUTRON(INDEX_CURRENT_COMP, x, y, z, vx, vy, vz, t, sx, sy, sz, p);
  }
%}

SAVE
%{
  DETECTOR_OUT_2D(
      "Divergence monitor",
      "X divergence [deg]",
      "Y divergence [deg]",
      -maxdiv_h, maxdiv_h, -maxdiv_v, maxdiv_v,
      nh, nv,
      &Div_N[0][0],&Div_p[0][0],&Div_p2[0][0],
      filename);
%}

FINALLY
%{
  destroy_darr2d(Div_N);
  destroy_darr2d(Div_p);
  destroy_darr2d(Div_p2);
%}

MCDISPLAY
%{
  multiline(5, (double)xmin, (double)ymin, 0.0,
               (double)xmax, (double)ymin, 0.0,
               (double)xmax, (double)ymax, 0.0,
               (double)xmin, (double)ymax, 0.0,
               (double)xmin, (double)ymin, 0.0);
%}

END
