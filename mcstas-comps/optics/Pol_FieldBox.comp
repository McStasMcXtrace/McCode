/*******************************************************************************
*
* McStas, neutron ray-tracing package
*         Copyright 1997-2002, All rights reserved
*         Risoe National Laboratory, Roskilde, Denmark
*         Institut Laue Langevin, Grenoble, France
*
* Component: Pol_FieldBox
*
* %I
*
* Written by: Erik B Knudsen and P Willendrup
* Date: 2013
* Version: $Revision$
* Release: McStas 2.0
* Origin: Risoe
*
* Box containing a constant or tabled magnetic field
*
* %D
*
*
* %P
* fieldtype: [ ] Should be either 1 (constant field in the box) or -1 (tabled in data file).
* xwidth:  [m] Width of the box containing the field.
* yheight: [m] Height of the box containing the field.
* zdepth:  [m] Depth of the box containing the field.
* Bx:  [T] Magnetic field strength along the x-axis.
* By:  [T] Magnetic field strength along the y-axis.
* Bz:  [T] Magnetic field strength along the z-axis.
* filename: [text] Name of file containing the magnetic field.
*
* %E
*******************************************************************************/

DEFINE COMPONENT Pol_FieldBox
DEFINITION PARAMETERS ()
SETTING PARAMETERS (int field_type=1,xwidth,yheight,zdepth, Bx,By,Bz, string filename="")
OUTPUT PARAMETERS ()
/* Neutron parameters: (x,y,z,vx,vy,vz,t,sx,sy,sz,p) */

SHARE
%{
    %include "pol-lib"
%}

DECLARE
%{
  struct field_parameters Bprms;
%}

INITIALIZE
%{
  enum field_functions ff=field_type;
  #ifdef USE_OFF
  mcmagnet_init();

  if(ff==tabled){
    struct interpolator_struct *interpolator = interpolator_load(filename, 3, 3, NULL);
    interpolator_info(interpolator);
    Bprms.generic = (void *)interpolator;
  } else 
  #endif
  if (ff==constant){
    /*constant magnetic field in the box*/
    Bprms.field_prms[0]=Bx;Bprms.field_prms[1]=By;Bprms.field_prms[2]=Bz;
  }
%}



TRACE
%{
    int hit;
    double t0,t1;
    if (hit=box_intersect(&t0,&t1,x,y,z,vx,vy,vz,xwidth,yheight,zdepth)){
        if(t0>0) PROP_DT(t0);
        //mcmagnet_push(field_type,&(ROT_A_CURRENT_COMP),&(POS_A_CURRENT_COMP),0,&Bprms);
        double phi_prec;
        if(t1-t0>0){
          PROP_DT(t1-t0);
          /*do the precession "manually"*/
          phi_prec= fmod(sqrt(Bx*Bx+ By*By+ Bz*Bz) * (t1-t0)*mc_pol_omegaL, 2*PI);
          double sx_i,sy_i,sz_i;
          sx_i=sx;sy_i=sy;sz_i=sz;
          rotate(sx, sy, sz, sx_i,sy_i,sz_i, phi_prec, Bx, By, Bz);
        }
        //mcmagnet_pop();
    }

%}

MCDISPLAY
%{
  const int nDash = 10;
  double xw_2,yh_2,zd_2;
  xw_2=xwidth/2.0;yh_2=yheight/2.0;zd_2=zdepth/2.0;
  /*entrance*/
  dashed_line(-xw_2, -yh_2, +zd_2,  xw_2, -yh_2, -zd_2, nDash);
  dashed_line(-xw_2, -yh_2, +zd_2, -xw_2,  yh_2, +zd_2, nDash);
  dashed_line( xw_2,  yh_2, -zd_2, -xw_2,  yh_2, +zd_2, nDash);
  dashed_line( xw_2,  yh_2, -zd_2,  xw_2, -yh_2, -zd_2, nDash);

  /*exit*/
  dashed_line(-xw_2, -yh_2, zdepth+zd_2,  xw_2, -yh_2, zdepth-zd_2, nDash);
  dashed_line(-xw_2, -yh_2, zdepth+zd_2, -xw_2,  yh_2, zdepth+zd_2, nDash);
  dashed_line( xw_2,  yh_2, zdepth-zd_2, -xw_2,  yh_2, zdepth+zd_2, nDash);
  dashed_line( xw_2,  yh_2, zdepth-zd_2,  xw_2, -yh_2, zdepth-zd_2, nDash);

  /*4 lines to make a box*/
  dashed_line(-xw_2, -yh_2, +zd_2, -xw_2, -yh_2, zdepth+zd_2, nDash);
  dashed_line(-xw_2,  yh_2, +zd_2, -xw_2,  yh_2, zdepth+zd_2, nDash);
  dashed_line( xw_2, -yh_2, -zd_2,  xw_2, -yh_2, zdepth-zd_2, nDash);
  dashed_line( xw_2,  yh_2, -zd_2,  xw_2,  yh_2, zdepth-zd_2, nDash);
%}

END
