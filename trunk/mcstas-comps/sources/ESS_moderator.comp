/*******************************************************************************
*
* Mcstas, neutron ray-tracing package
*         Copyright (C) 1997-2012, All rights reserved
*         Risoe National Laboratory, Roskilde, Denmark
*         Institut Laue Langevin, Grenoble, France
*
* Component: ESS_moderator
*
* %I
* Written by: P Willendrup and E Klinkby, February 2014, derived from K Lefmann ESS_moderator_long
* Modified by: 
* Version: $Revision$
* Origin: DTU
* Release: McStas 2.1
*
* A parametrised pulsed source for modelling ESS long pulses.
*
* %D
* Produces a time-of-flight spectrum, from the ESS parameters
* Chooses evenly in lambda, evenly/exponentially decaying in time
* Adapted from ESS_moderator_long by: K Lefmann, 2001
*
* 2014-updates:
* <ol>
* <li>Selectable between simple flat single-moderator (planar=1) and MCNPX-like dual-moderator (thermal and cold) setup.
* <b>IMPORTANT</b>: In case of the dual-moderator geometry, the origin of the component is inside the cylindrical 
* moderator, i.e. take care when positioning the next components! (E Klinkby)
* <li>The spectrum from the source(s) is defined via the sourcedef string input parameter which allows these values:
* <ul>
* <li>sourcedef="2001", legacy "Mezei moderators" from the original F. Mezei documents 
* "ESS reference moderator characteristics for generic instrument performance evaluation", but rescaled to ESS TDR frequency, pulselength and power.
* <li>sourcedef="TDR", Mezei moderators, with a wavelength-dependent correction term to the cold flux, derived from 
* 2012 MCNPX calculations by ESS neutronics group. Corrections calculated by K Lieutenant (Vitess) and 
* implemented here by E Klinkby. NOTE: uses the 2001 brilliance for the thermal moderator!
* <li>sourcedef="2014", updated brilliance using formulation by Troels Schoenfeldt, including support for the "pancacke", i.e. flat geometry.
*</ul>
* <li>The component can use target_index for focusing to a given beam port. Use an Arm() and ROTATED to position 
* relatively to the moderator.
* <li>Time focusing option: Adjusts neutron departure time to match a 'first chopper' defined by parameters tfocus_dist, tfocus_time, tfocus_width (K Lefmann).
* NOTE: Currently only applies to the Mezei-moderator.
* <li>The component relies on the new ess_source-lib which is expected to become further enriched during design-finaliziation and construciton of the ESS.
* </ol>
*
* Units of Brilliance: n/cm^2/s/AA/ster
* (McStas units are in general neutrons/second)
*
* Derived from ESS_moderator_long which was debugged intensively against Mezei note (4/12 2000) and VitESS @ Rencurel 2006.
*
* %VALIDATION 
* Validated against VitESS and Mezei note (4/12 2000) @ Rencurel 2006
*
* %P
* Input parameters:
*
* yheight_c: (m)    Height of the cylindershaped cold source
* yheight_t: (m)    Height of the rectangular thermal source
* cyl_radius:(m) Radius of the cylindershaped cold source - coupled to width_c
* width_t: (m)    Edge of cube shaped thermal source
* width_c: (m)    Arc-length opening of cylindershaped cold source - coupled to cyl_radius. (Used as width of rectangular geometry if planar=1)
* Lmin:   (AA)   Lower edge of wavelength distribution
* Lmax:   (AA)   Upper edge of wavelength distribution
* dist:   (m)    Distance from source to focusing rectangle; at (0,0,dist)
* focus_xw:(m)   Width of focusing rectangle
* focus_yh:(m)   Height of focusing rectangle
* target_index:(1)  relative index of component to focus at, e.g. next is +1
*                this is used to compute 'dist' automatically.
* n_pulses: (1)  Number of pulses simulated. 0 and 1 creates one pulse. 
*                The integrated intensity is constant 
* cold_frac: (1) Fraction of neutron statistics from cold source. It is implicitely assumed 
*                that supermirror allows each beamline to choose the desired fraction
*                of cold and thermal neutrons (i.e. extreme idealization).
* tfocus_dist: (m) Position of time window
* tfocus_time: (s) Time position of window
* tfocus_width: (s) Time width of window
* tmax_multiplier: (1) Defined maximum emission time at moderator, tmax= tmax_multiplier * ESS_PULSE_DURATION. Only in combination with sourcedef="2013" or "2014"
* beamport_angle: (deg) Direction within the beamport sector (0 < angle < extraction_width) to direct neutrons. If set to -1, is recalculated to extraction_width/2.
* extraction_width: (deg) Width of extraction-area in degrees (60 or 120 degrees). 120 deg only in combination with sourcedef="2014"
* acc_power: (MW) Accelerator power in MW
* sourcedef: (string) ESS source "database", values: "TDR", "2001", "2013", "2014"
* planar: (boolean) If set to 1, source geometry is flat and rectangular. Can only be used if cold_frac is 1 or 0, i.e. for fully thermal or fully cold source. May overwrite other geometry input.
* isleft: (boolean) If set to 1, place thermal wing left of cold cylinder, if set to 0, place thermal wing right of cold cylinder.
* uniform: (boolean) If set ot 0, applies vertical and horizontal anisotropy of brilliance. Only in combination with sourcedef="2014"
*
* %E
*******************************************************************************/

DEFINE COMPONENT ESS_moderator
DEFINITION PARAMETERS ()
SETTING PARAMETERS (isleft=0.9,
		    Lmin, Lmax, cold_frac=1.0, dist=0, focus_xw, focus_yh, int target_index=0, tmax_multiplier=3,yheight_c=0.12, yheight_t=0.12,
		    int n_pulses=1, acc_power=5, beamport_angle=-1, string sourcedef="2014", xwidth_c=0.23, xwidth_t=0.12, extraction_opening=120)
//OUTPUT PARAMETERS (cold_bril, thermal_bril, l_range, w_mult, w_geom, w_geom, w_geom_t, yheight_c, yheight_t, cold, tmax_multiplier, uniform)
SHARE
%{
  %include "ess_source-lib"
%}

DECLARE
%{
  double l_range, w_mult, w_geom, w_geom_c, w_geom_t, w_stat, Wconst;
  int cold;
  int wasleft;
  int cosine;
  double tx,ty,tz;
  double cx,cy,cz,cyl_radius;
  double t1x,t1y,t1z,t2x,t2y,t2z;
  
  /* Target station geometry... */
  ess_moderator_struct modextras;
  functype cold_bril;
  functype thermal_bril;
  double tfocus_width,  tfocus_time,  dt;
  double r_empty;
%}

INITIALIZE
%{
  /* Weight multipliers */
  w_mult=acc_power/5;
  w_geom=1;
  w_geom_c=1;
  w_geom_t=1;
  Wconst=1;
  w_stat=1.0/mcget_ncount();
  
  tfocus_width=0;  tfocus_time=0;  dt=0;

  n_pulses=(double)floor(n_pulses);
  if (n_pulses == 0) n_pulses=1;
  
  if (target_index && !dist) {
    Coords ToTarget;
    ToTarget = coords_sub(POS_A_COMP_INDEX(INDEX_CURRENT_COMP+target_index),POS_A_CURRENT_COMP);
    ToTarget = rot_apply(ROT_A_CURRENT_COMP, ToTarget);
    coords_get(ToTarget, &tx, &ty, &tz);
    dist=sqrt(tx*tx+ty*ty+tz*tz);
  } else if (target_index && !dist) {
    printf("ESS_moderator: %s: Please choose to set either the dist parameter or specify a target_index.\nExit\n", NAME_CURRENT_COMP);
    exit(-1);
  } else {
    tx=0, ty=0, tz=dist;
  }
  
  if (focus_xw < 0 || focus_yh < 0) {
    printf("ESS_moderator: %s: Please specify both focus_xw and focus_yh as positive numbers.\nExit\n", NAME_CURRENT_COMP);
    exit(-1);
  }
    
  if (Lmin<=0 || Lmax <=0 || dist == 0)
  {
    printf("ESS_moderator: %s: Check parameters (lead to Math Error).\n Avoid 0 value for {Lmin Lmax dist} \n", NAME_CURRENT_COMP);
    exit(-1);
  }
  
  
  /* ESS-2014 definition: */
  /* Geometry parameters */
  if (strcasestr(sourcedef,"2014")) {
    if (xwidth_c!=0.23 || xwidth_t!=0.12 || extraction_opening!=120) {
      fprintf(stderr,"FATAL: sourcedef %s only allows xwidth_c=0.23, xwidth_t=0.12 and extraction_opening=120 !\n",sourcedef);
      exit(-1);
    }
    cold_bril=ESS_2014_Schoenfeldt_cold;
    thermal_bril=ESS_2014_Schoenfeldt_thermal;
    cx = 0; cy=0; cz=-0.12; cyl_radius=0.12;
    t1x=sin(DEG2RAD*120/2);t1z=cos(DEG2RAD*120/2);
    t2x=-sin(DEG2RAD*120/2);t2z=cos(DEG2RAD*120/2);
    t1y=0; t2y=0;
    r_empty=2;
  } else {
    fprintf(stderr,"FATAL: sourcedef %s is not defined!\n",sourcedef);
    exit(-1);
  }
  modextras.height_c=yheight_c;
  modextras.Width_c=xwidth_c;
  modextras.Width_t=xwidth_t;
  modextras.height_t=yheight_t;
  modextras.tmultiplier=tmax_multiplier;
  modextras.extractionangle=extraction_opening;
  
  /* Is beamport_angle set at a meaningful angle - otherwise recalc */
  if ( beamport_angle<0 || beamport_angle>modextras.extractionangle){
    beamport_angle = modextras.extractionangle/2.0;
    printf("%s: WARNING: Resetting your beamport_angle to %g (%g/2) - central beamslot\n",NAME_CURRENT_COMP,beamport_angle,modextras.extractionangle);
  }
  if (strcasestr(sourcedef,"2014") && beamport_angle != modextras.extractionangle/2.0) {
    printf("%s: WARNING: beamport_angle is not at central slot. With sourcedef %s this means application of a partially analytical brightness model for B(\\theta) \n",NAME_CURRENT_COMP,sourcedef);
  }
  modextras.beamportangle=beamport_angle;
  
  l_range = Lmax-Lmin;
  w_geom_c  = xwidth_c*yheight_c*1.0e4;     /* source area correction */
  w_geom_t  = xwidth_t*yheight_t*1.0e4;
  w_mult *= l_range;            /* wavelength range correction */

  /* Calculate constant solid-angle for object, as seen from comp origo */
  double ttmp;
  
  randvec_target_rect_real(&ttmp, &ttmp, &ttmp, &Wconst,
  			   tx, ty, tz, focus_xw, focus_yh, ROT_A_CURRENT_COMP, 0, 0, 0, 0);
%}
TRACE
%{
  p=1;
  double v,E,lambda,k,r,xf,yf,dx,dy,dz,w_focus,tail_flag,cor,dt,xprime,yprime,zprime;

  /* Bispectral source - choice of spectrum and initial position */
  cold = ( rand01() < cold_frac );
  
  /* Use standard McStas routine or -not for specifying emission directionality */
  cosine = 0;
  
  /* Geometry adapted from ESS MCNPX model, mid 2012 */
  if (cold) {          //case: cold moderator
    x = 0.5*randpm1()*xwidth_c;
    y = 0.5*randpm1()*yheight_c;
    z = 0;
    cosine = 0;
    w_geom = w_geom_c;
  }  else  {
    //case: thermal moderator
    y = 0.5*randpm1()*yheight_t;
    if (rand01()<isleft) {
      wasleft=1;
      x = -xwidth_c/2.0 - rand01()*xwidth_t;
    } else {
      wasleft=0;
      x = xwidth_c/2.0 + rand01()*xwidth_t;
    }
    z = 0;
    // cosine = 1;
    cosine = 0;
    w_geom = w_geom_t;
  }
  
  randvec_target_rect_real(&xf, &yf, &r, &w_focus,
			   tx, ty, tz, focus_xw, focus_yh, ROT_A_CURRENT_COMP, x, y, z, cosine);
  
  /* In case of "complicated" geometry, calculate angular-weighting by independently from randvec */
  if (cosine==0) {
    if (cold) {
      //w_focus=1;
      /* Uniform emission */
    } else {
      /* Apply a cosine wrt. beamport angle vs. normal of the thermal extensions */
      if (wasleft==1) {
	//w_focus = Wconst ;//* cos(DEG2RAD*(beamport_angle-90))/cos(DEG2RAD*extraction_width/2);
      } else {
	//w_focus = Wconst ;//* cos(DEG2RAD*((extraction_width - beamport_angle)-90))/cos(DEG2RAD*extraction_width/2);
      }
    }
  }
  
  dx = xf-x;
  dy = yf-y;
  dz = r-z;
  r = sqrt(dx*dx+dy*dy+dz*dz);
  
  lambda = Lmin+l_range*rand01();    /* Choose from uniform distribution */
 
  k = 2*PI/lambda;
  v = K2V*k;

  vz = v*dz/r;
  vy = v*dy/r;
  vx = v*dx/r;
  
  modextras.X=x; modextras.Y=y; modextras.Z=z; modextras.Wasleft=wasleft;
  
  if (cold) {          //case: cold moderator
    cold_bril( &t,  &p,  lambda,  tfocus_width,  tfocus_time,  dt, modextras);
  }  else  {                      //case: thermal moderator
    thermal_bril( &t,  &p,  lambda,  tfocus_width,  tfocus_time,  dt, modextras);
  }
  
  w_focus=focus_xw*focus_yh/sqrt(tx*tx+ty*ty+tz*tz);
  // printf("Weights %g %g %g %g %g %g\n",p ,w_stat ,cold_frac ,w_focus ,w_geom, w_mult);
  // Solid angle of object as seen from origo...

  p*=w_stat*w_focus*w_geom*w_mult;
  //  p*=w_focus*w_geom*w_mult;
  t+=(double)floor((n_pulses)*rand01())/ESS_SOURCE_FREQUENCY;   /* Select a random pulse */

  /* Correct weight for sampling of cold vs. thermal events. */
  if (cold) {
    p /=cold_frac;
    //p=1;
      /* Correct position to actual physical emission location on cylinder (propagate back from T. Schoenfeldts plane */
      /* ALLOW_BACKPROP; */
      /* double t1,t2; */
      /* cylinder_intersect(&t1, &t2, x, y, z, vx, vy, vz, cyl_radius-1e-6, yheight_c); */
      /* printf("Times... %g %g\n",t1,t2); */
      /* PROP_DT(fmax(t1,t2)); */
      /* DISALLOW_BACKPROP; */
  } else {
    p/=(1-cold_frac);
    if (wasleft) {
      p/=(isleft);
    } else {
      p/=(1-isleft);
    }
  }
  //printf("Weights %g %g %g %g %g %g\n",p ,w_stat ,cold_frac ,w_focus ,w_geom, w_mult);
  /* if (!planar) { */
  /*   double t1,t2; */
  /*   cylinder_intersect(&t1, &t2, x, y, z, vx, vy, vz, cyl_radius-1e-6, yheight_c); */
  /*   if(t1>0 || t2>0) {PROP_DT(fmax(t1,t2)); SCATTER; ABSORB;} */
  /* } */

%}

MCDISPLAY
%{
  /* if (planar==0) { */
  /* Draw cold moderator as cylinder */
  magnify("xz");
  circle("xz", cx,  cy+yheight_c/2.0, cz, cyl_radius);
  circle("xz", cx,  cy-yheight_c/2.0, cz, cyl_radius);
  line(cx, cy-yheight_c/2.0, cz+cyl_radius, cx, cy+yheight_c/2.0, cz+cyl_radius);
  line(cx, cy-yheight_c/2.0, cz-cyl_radius, cx, cy+yheight_c/2.0, cz-cyl_radius);
  line(cx+cyl_radius, cy-yheight_c/2.0, cz, cx+cyl_radius, cy+yheight_c/2.0, cz);
  line(cx-cyl_radius, cy-yheight_c/2.0, cz, cx-cyl_radius, cy+yheight_c/2.0, cz);
  
    /* Draw thermal moderators as a couple of squares + some lines */

    // Left
    multiline(5, cx+t1x*cyl_radius, cy-yheight_t/2.0, cz+t1z*cyl_radius,
  	      cx+t1x*cyl_radius + t1x*xwidth_t, cy-yheight_t/2.0, cz+t1z*cyl_radius + t1z*xwidth_t,
  	      cx+t1x*cyl_radius + t1x*xwidth_t, cy+yheight_t/2.0, cz+t1z*cyl_radius + t1z*xwidth_t,
  	      cx+t1x*cyl_radius, cy+yheight_t/2.0, cz+t1z*cyl_radius, cx+t1x*cyl_radius, cy-yheight_t/2.0, cz+t1z*cyl_radius);
    // Right
    multiline(5, cx+t2x*cyl_radius, cy-yheight_t/2.0, cz+t2z*cyl_radius,
	      cx+t2x*cyl_radius + t2x*xwidth_t, cy-yheight_t/2.0, cz+t2z*cyl_radius + t2z*xwidth_t,
	      cx+t2x*cyl_radius + t2x*xwidth_t, cy+yheight_t/2.0, cz+t2z*cyl_radius + t2z*xwidth_t,
	      cx+t2x*cyl_radius, cy+yheight_t/2.0, cz+t2z*cyl_radius, cx+t2x*cyl_radius, cy-yheight_t/2.0, cz+t2z*cyl_radius);
    
    /* Dashed lines for indicating "beam extraction" area... */
    dashed_line(cx+t1x*cyl_radius,cy-yheight_c/2.0, cz+t1z*cyl_radius, cx+t1x*r_empty, cy-yheight_c/2.0, cz+t1z*r_empty,10);
    dashed_line(cx+t1x*cyl_radius, cy+yheight_c/2.0, cz+t1z*cyl_radius, cx+t1x*r_empty, cy+yheight_c/2.0, cz+t1z*r_empty,10);
    dashed_line(cx+t2x*cyl_radius, cy-yheight_c/2.0, cz+t2z*cyl_radius, cx+t2x*r_empty, cy-yheight_c/2.0, cz+t2z*r_empty,5);
    dashed_line(cx+t2x*cyl_radius, cy+yheight_c/2.0, cz+t2z*cyl_radius, cx+t2x*r_empty, cy+yheight_c/2.0, cz+t2z*r_empty,5);
    
    
    /* Circles indicating extent of the "empty" zone where optics is not allowed */
    circle("xz", cx,  cy+yheight_c/2.0, cz, r_empty);
    circle("xz", cx,  cy-yheight_c/2.0, cz, r_empty);
    
    /* Circles indicating the builk shielding of the target monolith at 6 m */
    circle("xz", cx,  cy+focus_yh/2.0 , cz, 6);
    circle("xz", cx, cy-focus_yh/2.0 , cz, 6);
    circle("xz", cx,  cy+2, cz, 6);
    circle("xz", cx, cy-2, cz, 6);
    
    /* This last bit is relevant only in connection with 2014 source definition */
    if (strcasestr(sourcedef,"2014")) {
      /* Arrow indicating proton beam direction */
      line(cx-5.9,cy,cz,cx-cyl_radius-0.1,cy,cz);
      line(cx-cyl_radius-0.2*cos(10*DEG2RAD),cy,cz+0.2*sin(10*DEG2RAD),cx-cyl_radius-0.1,cy,cz);
      line(cx-cyl_radius-0.2*cos(10*DEG2RAD),cy,cz-0.2*sin(10*DEG2RAD),cx-cyl_radius-0.1,cy,cz);
      
      /* Rectangle indicating the chosen focus rectangle - where the optics starts... */
      rectangle("xy",tx,ty,tz,focus_xw,focus_yh);
      rectangle("xy",0,0,0,xwidth_c,yheight_c);
      rectangle("xy",-xwidth_c/2-xwidth_t/2,0,0,xwidth_t,yheight_t);
      rectangle("xy",xwidth_c/2+xwidth_t/2,0,0,xwidth_t,yheight_t);
      rectangle("xy",tx,ty,tz,focus_xw,focus_yh);
      
    }
%}

END
