/*******************************************************************************
*
* McStas, neutron ray-tracing package
*         Copyright (C) 1997-2008, All rights reserved
*         Risoe National Laboratory, Roskilde, Denmark
*         Institut Laue Langevin, Grenoble, France
*
* Instrument: ILL_IN4
*
* %Identification
* Written by: <a href="mailto:farhi@ill.fr">Emmanuel Farhi</a>
* Date: 2015.
* Origin: <a href="http://www.ill.fr">ILL (France)</a>
* Release: McStas 2.1
* Version: $Revision: 5055 $
* %INSTRUMENT_SITE: ILL
*
* The IN4 thermal Time-of-Flight spectrometer at the ILL (H12 tube).
*
* %Description
* IN4C is a high-flux time-of-flight spectrometer used for the study of excitations
* in condensed matter. It works in the thermal neutron energy range (10-100 meV).
*
* Primary spectrometer
*
* The main components of the beam conditioning part are the two background
* choppers, the double curvature mono-chromator with four faces and the Fermi
* chopper. The background choppers are rapidly pulsating beam shutters which act
* as a low-pass filter. Thus they eliminate from the beam most of the fast
* neutrons and gamma rays that would give background noise in the spectra. The
* modular shielding encloses the background choppers in separate compartments in
* order to cut off these undesired neutrons as early as possible. A suitable
* energy is selected from the thermal neutron spectrum with the crystal
* monochromator. The monochromator, an assembly of 55 crystal pieces,
* concentrates the divergent incident beam onto a small area at the sample
* position. The full use of the available solid angle gives a high incident
* flux. The vertical curvature is fixed, and the horizontal 
* variable curvature of the monochromator is essential in controlling
* the time and space focussing conditions for optimal performance (see H. Mutka,
* Nucl. Instr. and Meth. A 338 (1994) 144). The Fermi chopper rotates at speeds
* of up to 40000 rpm. It transmits short neutron pulses (10 ... 50 µs) to the
* sample. The time-of-flight of neutrons between the chopper and the sample (1
* ... 5 ms) can be measured by using precise electronic circuitry. 
* A sapphire (Al2O3) filter can be inserted in the beam to remove the fast neutrons
* background.
* 
* Monochromators: 
* PG       002 DM=3.355 AA (Highly Oriented Pyrolythic Graphite)
* PG       004 DM=1.607 AA (used for lambda=1.1)
* PG       006 DM=1.118 AA
* Cu       220 DM=1.278 AA
* Cu       111 DM=2.095 AA
* Take-off:       39-65 deg
* flux at sample: 5e5 n/s/cm2
*
* Secondary spectrometer
*
* The sample environment is designed to accommodate standard
* cryostats and furnaces. A radial collimator around the sample position is used
* to cut the scattering from the sample environment. The secondary flight-path
* is in vacuum to avoid parasitic scattering of the transmitted neutrons. The
* detector bank covers scattering angles of up to 120°. In addition to the 3He
* detector tubes (length 300 mm, width 30 mm, elliptical section, pressure 6
* bar) a 3He filled multidetector (eight sectors with 12 radial cells each;
* outer diameter Å 60 cm) will allow us to observe forward scattering. The
* time-of-flight spectra measured at various angles are further treated in order
* to obtain the scattering function S(Q,w) using e.g. LAMP.
*
* In this model, the sample is an Incoherent scatterer (e.g. Vanadium).
*
* %Example: lambda=2.2
*
* %Parameters
* INPUT PARAMETERS:
*   lambda:        [Angs] wavelength
*   dlambda:       [Angs] wavelength HALF spread at source
*   DM:            [Angs] monochromator d-spacing
*   ETAM:          [arcmin] monochromator mosaic FWHM
*   RMH:           [m]    Monochromator horizontal curvature. Use -1 for auto.
*   ratio:         [1]    Disk Chopper ratio (nu=nu_FC/ratio)
*   dE:            [meV]  Inelastic energy for time focusing
*   Sapphire_present: [1] Flag, when 1 the Al2O3 filter is active
*
* %Link
* H. Mutka, Nucl. Instr. and Meth. A 338 (1994) 144
* %Link
* http://www.ill.eu/fr/instruments-support/instruments-groups/instruments/in4c
* %End
*******************************************************************************/

DEFINE INSTRUMENT ILL_IN4(lambda=1.1, dlambda=1, DM=3.355, 
  ETAM=35, RMH=-1, ratio=4, dE=0, Sapphire_present=0)

DECLARE %{
  double A1, RMH, Frequency, LOM, phase;
  double LFS       = 0.675; /* FC to Sample distance */
  double LSD       = 2;     /* Sample to Detector distance */
  double LVS       = 0.583; /* BC1 to VS distance */
  
  /* the following variables are computed in the IN4 configuration */
  double d0, d2; /* IN4c distances used in NoMad/IN4 doc (Ross/Rols) */
  double phase2F   = 0;
  double nu        = 0;
  double LMS       = 0; /* [m]    Mono-sample distance (aka d1) */
  double LSM       = 0; /* [m]    Distance from source to monochromator. */
  double bctr      = 0; /* [m]    background chopper BC2 translation from BC1 */
  double phase12   = 0; /* [deg]  Chopper phase BC2 wrt BC1 */
%}

INITIALIZE %{
  double Ei, Vi, Ki;
  double t2F=0;
  double t12=0;
  double thetaB;
  double dSx=5.556;   /* BC1 to sample along beam tube axis */
  double dSy=1.3;     /* lateral position of sample */
  
  thetaB = -asin(lambda/DM/2);
  A1     = thetaB*RAD2DEG;
  
  Ki = 2*PI/lambda;
  Vi = K2V*fabs(Ki);
  Ei = VS2E*Vi*Vi;
  
  /* IN4c configuration */
  
  
  /* compute distances for IN4 */
  d2 = fabs(dSy/tan(2*thetaB));
  LMS= fabs(dSy/sin(2*thetaB)); /* Monok to Sample = d1 */
  d0 = dSx - d2 - LVS;    /* VS to Monok */ 
  
  /* set distances for IN4c */
  LSM = 6.1 + LVS + d0;
  
  /* FC rotation frequency (Master) */
  nu  = K2V/( fabs(DM*cos(thetaB)) * (LFS+LSD*pow( 1-dE/Ei, -3/2)) *(1-LMS/d0) );

  phase12 = 22.5;
  /* compute position of BC2 and phases */
  t12 = phase12/360/(nu/ratio); /* time delay [s] */

  bctr  = t12*Vi;
  if (bctr > 2.965) { /* chopper BC2 at its maximum position, from BC1 */
    bctr = 2.965; 
  }

  /* compute back the phases (in case bctr has changed) */
  phase12 = bctr*360*(nu/ratio)/Vi;
  /* distance BC2-FC */
  t2F     = ((LMS-LFS)+(LSM-6.1-bctr))/Vi;
  phase2F = t2F*360*nu;
  
  if (RMH < 0) {
    double L = 1/(1/d0+1/LMS); /* Monok optical focusing distance */
    RMH= 2*L/sin(DEG2RAD*A1);  /* RH = 2*L/sin(DEG2RAD*A1); */
  }
    
  Frequency= nu;
  LOM      = LSM - 5.2; /* from Obt1 to Monok */
  
  if (dlambda >= lambda)
    dlambda = lambda*.95;

  MPI_MASTER(
  /* print some information when starting */
  printf("%s: Thermal ToF spectrometer\n", NAME_CURRENT_COMP);
  printf("  Divergence at the lead shutter: %g [deg]\n", atan2(0.2,5.2)*RAD2DEG);
  printf("  Take-off at monochromator:      %g [deg] (A1)\n", A1);
  printf("  Incident energy:                %g [meV] (Ei)\n", Ei);
  printf("  Incident velocity:              %g [m/s] (Vi)\n", Vi);
  printf("  Source-Mono distance:           %g [m] (LSM)\n", LSM);
  printf("  Virtual Source-Mono distance:   %g [m] (d0)\n", d0);
  printf("  Mono-Sample distance:           %g [m] (d1=LMS)\n", LMS);
  printf("  Curvature at monochromator:     %g [m] (RMH)\n", RMH);
  printf("  Fermi Chopper Frequency:        %g [Hz] (nu) %g [rpm]\n", nu, nu*60);
  printf("  BC1-BC2 distance:               %g [m] (BCTR)\n", bctr);
  printf("  BC2 phase wrt BC1:              %g [deg] (deltaPhi)\n", phase12);
  printf("  FC phase wrt BC1:               %g [deg]\n", phase12+phase2F);
  );
  
  /* print a visual representation of distances */
  printf("[H12] %g [BC1] %g [VS] %g [BC2] %g [PG] %g [FC] %g [Spl]\n",
    6.1, LVS, bctr - LVS, LSM - (6.1 + bctr), LMS - LFS, LFS);
    
  /* tests for consistency */
  if (nu < 0 || nu > 700) exit(printf("Invalid: Fermi master frequency. Change DM ?\n"));
  if (fabs(A1) < 15)      exit(printf("Invalid: mono take-off angle. Change DM ?\n"));
  if (bctr < 0)           exit(printf("Invalid: BC1-BC2 distance. Change DM ?\n"));
  
%}


TRACE

COMPONENT Origin = Progress_bar()
  AT (0, 0, 0) ABSOLUTE

COMPONENT Thermal = Source_gen(
  radius   = 0.10/2,
  focus_xw = 0.1,
  focus_yh = 0.1,
  target_index=+1,
  lambda0  = lambda,
  dlambda  = dlambda,
  T1=683.7,I1=0.5874e+13,T2=257.7,I2=2.5099e+13,T3=16.7 ,I3=1.0343e+12,
  verbose  = 1)
  AT (0, 0, 0) RELATIVE Origin

/* bouchon barillet Phi=100mm */
COMPONENT Obt1 = Monitor_nD(
  xwidth=0.1, yheight=0.1, options="disk, slit, x y", bins=100)
  AT (0, 0, 5.2) RELATIVE Thermal
  
COMPONENT Obt1_lambda = Monitor_nD(xwidth=0.1, yheight=0.1, options="lambda limits=[.1 3]", bins=100)
  AT (0, 0, 0.01) RELATIVE Obt1

/* sapphire filter to remove fast neutrons
   c along beam axis, e=90 60x110 mm
 */

COMPONENT SapphireFilter = Filter_gen(xwidth=0.12, yheight=0.12, 
  filename="Al2O3_sapphire.trm", thickness=2)
  AT (0,0,0.2) RELATIVE Obt1

COMPONENT Win1 = Monitor_nD(
  xwidth=0.12, yheight=0.12, options="disk, slit, x y", bins=100)
  AT (0,0, 0.4) RELATIVE Obt1
  
COMPONENT Win1_lambda = Monitor_nD(xwidth=0.1, yheight=0.1, options="lambda limits=[.1 3]", bins=100)
  AT (0, 0, 0.01) RELATIVE Win1

/* BC1 should be as early as possible. Opening of slits: 22.5 deg i.e. 6 cm */
COMPONENT BC1  = DiskChopper(radius=0.65/2, nslit=8, isfirst=1, 
  theta_0=22.5, nu=nu/ratio, yheight=0.12)
  AT (0,0, 6.1) RELATIVE Thermal
 
/* the "Virtual Source" (which is just a slit) */
/* the monochromator makes an image of it onto the sample */
COMPONENT VS   = Slit(xwidth=0.08, yheight=0.20)
AT (0,0, 0.583) RELATIVE BC1

COMPONENT BC2  = DiskChopper(radius=.75/2, nslit=8, theta_0=22.5, 
  nu=nu/ratio, phase=phase12, yheight=0.12)
AT (0,0, bctr) RELATIVE VS

COMPONENT Cradle = Monitor_nD(
  options="x y", bins=50, xwidth=.25, yheight=.25, restore_neutron=1)
  AT (0,0,LSM) RELATIVE Thermal

COMPONENT Cradle_lambda = Monitor_nD(
  options="lambda limits=[.1 3]", bins=100, xwidth=.25, yheight=.25, restore_neutron=1)
  AT (0,0,LSM+0.01) RELATIVE Thermal

SPLIT COMPONENT Mono = Monochromator_curved(
  width=0.22, height=0.2, NH=5, NV=11, RV=-1.9, RH=RMH,
  DM=DM, mosaic=ETAM)
  AT (0,0,0) RELATIVE Cradle
  ROTATED (0, A1, 0) RELATIVE Cradle
  
COMPONENT Mono_out = Arm()
  AT (0,0,0) RELATIVE Cradle
  ROTATED (0, 2*A1, 0) RELATIVE Cradle
  
COMPONENT FC_Pos = Arm()
AT (0,0,LMS-0.675) RELATIVE Mono_out

COMPONENT Mon_FC_Pos = Monitor_nD(
  xwidth = 0.05, yheight = 0.064,
  options="t slit, all auto", bins=512)
AT (0,0,-0.025-0.001) RELATIVE FC_Pos
  
COMPONENT Fermi = FermiChopper(phase=phase12+phase2F, radius=0.025, nu=nu,
   yheight=0.064, xwidth=0.023, nslit=50, length=0.025)
AT (0,0,0) RELATIVE FC_Pos
EXTEND %{
    if (!SCATTERED) ABSORB;
  %}
  
COMPONENT Sample_pos = Arm()
  AT (0,0,LMS) RELATIVE Mono_out

COMPONENT sample_flux = Monitor_nD(
    xwidth = 0.1, yheight = 0.1, options = "x y", 
    restore_neutron=1, bins=100)
  AT (0, 0, 0) RELATIVE Sample_pos
  
COMPONENT sample_tof = COPY(sample_flux)(options = "x, auto time")
  AT (0, 0, 0) RELATIVE Sample_pos
  
COMPONENT sample_lambda = COPY(sample_flux)(options = "lambda limits=[.1 3]")
  AT (0, 0, 0) RELATIVE Sample_pos
  
SPLIT COMPONENT Sample = Incoherent(radius=0.01/2, yheight=0.05, focus_ah=RAD2DEG*atan2(2, 2.5))
  AT (0,0,0) RELATIVE Sample_pos
  EXTEND %{
    if (!SCATTERED) ABSORB;
  %}
  
COMPONENT Detector = Monitor_nD(radius=2.5, yheight=2,
  options="theta limits=[-15 135] bins=300, y bins=100, banana")
  AT (0,0,0) RELATIVE Sample_pos

END

