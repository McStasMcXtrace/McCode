/************************************************
*
* Test TOF-diffraction instrument for the Sample_nxs.comp component
*
* %Identification
* Written by: Mirko Boin (HZB)
*
* Date: 2012
* Origin: HZB
* Release: McStas 2.0
* Version: $Revision: 1.4 $
* %INSTRUMENT_SITE: HZB
*
* %Description
* Test TOF-diffraction instrument for the Sample_nxs.comp component. See documentation for the component plus the references in the Link section below.
*
* This component/instrument relies on the 'nxs' library that is provided with McStas as of release 2.0.
* Please see further instructions in your MCSTAS/lib/share/nxslib/README.TXT
*
* Example:
* mcrun Test_Sample_nsx_diffraction.instr
*
* %Parameters
* lambda1: Lower bound of wavelength band  [AA]
* lambda2: Upper bound of wavelength band  [AA]
* sample_detector_distance: Distance between sample and detector [m]
* moderator_pinhole_distance: Distance between moderator and pinhole [m]
* pinhole_sample_distance: Distance between pinhole and sample [m]
* sample_x: Sample translation along x [m]
* sample_file: Crystallographic structure from 'nsx' file [str]
* moderator_face: Chosen ISIS moderator [str]
*
* %Link
* M. Boin (2012), <i>J. Appl. Cryst.</i> <b>45</b>, 603-607, <a href="http://dx.doi.org/10.1107/S0021889812016056">doi:10.1107/S0021889812016056</a>
* M. Boin, R.C. Wimpory, A. Hilger, N. Kardjilov, S.Y. Zhang, M. Strobl (2012), <i>J. Phys.: Conf. Ser.</i> <b>340</b>, 012022, <a href="http://dx.doi.org/10.1088/1742-6596/340/1/012022">doi:10.1088/1742-6596/340/1/012022</a>
* M. Boin, A. Hilger, N. Kardjilov, S.Y. Zhang, E.C. Oliver, J.A. James, C. Randau, R.C. Wimpory (2011), <i>J. Appl. Cryst.</i> <b>44</b>, 1040-1046, <a href="http://dx.doi.org/10.1107/S0021889811025970">doi:10.1107/S0021889811025970</a>
*
* %End
***************************************************/
DEFINE INSTRUMENT Test_Sample_nxs_diffraction(lambda1=1.5, lambda2=1.51,
                               sample_detector_distance=0.1,
                               moderator_pinhole_distance=30.0,
                               pinhole_sample_distance=0.05,
                               sample_x = 0.0,
                               string sample_file="Cu.nxs",
                               string moderator_face="water")

DECLARE
%{
%}

INITIALIZE
%{

%}
TRACE

COMPONENT a1 = Arm()
  AT (0,0,0) ABSOLUTE


COMPONENT moderator = ISIS_moderator(
  Face = moderator_face, Emin = -lambda1, Emax = -lambda2, dist = 4.0,
  focus_xw = 0.001, focus_yh = 0.001, xwidth = 0.001, yheight = 0.001,
  CAngle = 0.0, SAC = 1
) AT (0.0, 0.0, 0.0) RELATIVE a1


COMPONENT pinhole = Slit(
  xwidth = 0.002,
  yheight = 0.002
) AT (0.0, 0.0, moderator_pinhole_distance) RELATIVE moderator


COMPONENT sample1 = Sample_nxs(
  yheight = 0.005,
  radius = 0.0025,
  nxsFileName = sample_file,
  max_hkl=8,
  TransOnly = 0,
  IncohScat = 1,
  MultiScat = 0,
  d_phi = 110.0
) AT (sample_x, 0.0, pinhole_sample_distance) RELATIVE pinhole ROTATED (0,0,0) ABSOLUTE

COMPONENT sample = Arm()
  AT (0,0,0) RELATIVE sample1 ROTATED (0,0,0) RELATIVE sample1

COMPONENT beamstop = Beamstop(
  radius = 0.005
  ) AT (-sample_x, 0, 0.05) RELATIVE sample


  /*COMPONENT psd4pi = MyPSD_monitor_4PI(
  radius=0.5,
  nx=300, ny=300,
  filename="psd4pi.psd",
  tth_file="diffractogram.dat",
  restore_neutron = 1
  ) AT (-sample_x, 0, 0) RELATIVE sample */

COMPONENT psd4pi = PSD_monitor_4PI(
  radius=0.5,
  nx=300, ny=300,
  filename="psd4pi.psd",
  restore_neutron = 1
  ) AT (-sample_x, 0, 0) RELATIVE sample



COMPONENT tofmon = TOF_monitor(
  nt = 600,
  filename = "tofmon.dat",
  xwidth =0.1,
  yheight = 0.1,
  tmin = 25000,
  tmax = 45000,
  restore_neutron = 1
) AT (-sample_x, 0, sample_detector_distance) RELATIVE sample

















COMPONENT lmon = L_monitor(
  nL = 500,
  filename = "lmon.dat",
  xwidth = 0.1,
  yheight = 0.1,
  Lmin = lambda1-lambda2,
  Lmax = lambda1+lambda2,
  restore_neutron = 1
) AT (-sample_x, 0, sample_detector_distance) RELATIVE sample

COMPONENT rot90 = Arm()
  AT (0,0,0) RELATIVE sample ROTATED (0,60,0) RELATIVE sample

COMPONENT blende = Slit(
  xwidth = 0.001,
  yheight = 0.1002
) AT (0.0, 0.0, 0.026) RELATIVE rot90

COMPONENT detector = PSD_monitor(
    nx = 500, ny = 500, filename = "PSD.dat", xmin = -0.5,
    xmax = 0.5, ymin = -0.3, ymax = 0.3)
  AT (0, 0, 1.0) RELATIVE rot90

FINALLY

%{
%}

END
