/* Instrument: Test_Isotropic_Sqw.instr
 *
 * %Identification
 * Written by: E. Farhi
 * Date: Jan 2004
 * Origin: ILL
 *
 * %INSTRUMENT_SITE: tests
 *
 * A test instrument for the S(q,w) sample
 *
 * %Description
 * This instrument is a test instrument for the S(q,w) sample.
 * It produces a tof-angle detector
 *
 *
 * Example: mcrun Test_Isotropic_Sqw.instr lambda=3.4
 *
 * %Parameters
 * INPUT PARAMETERS:
 *    lambda:      source energy                               [Angs]
 *    sample_coh:  name of coherent Sqw data file              [str]
 *    sample_inc:  name of incoherent Sqw data file            [str]
 *    interpolate: flag to interpolate Sqw tables              [1]
 *
 * %Link
 * The Isotropic_Sqw sample
 * %End
*/

DEFINE INSTRUMENT Test_Isotropic_Sqw(lambda=3.4,
  string sample_coh="Rb_liq_coh.sqw", string sample_inc="Rb_liq_inc.sqw", interpolate=1)

TRACE

COMPONENT Origin = Progress_bar(percent=5)
  AT (0,0,0) ABSOLUTE

COMPONENT Source=Source_gen(
        radius=0.01, xw=0.01, yh=0.02, dist=1.5, Lambda0=lambda, dLambda=0.01*lambda)
AT (0,0,0) RELATIVE Origin

COMPONENT PSD=Monitor_nD(options="x,y, auto, parallel")
AT (0,0,0) RELATIVE PREVIOUS

/* sample position */
COMPONENT Sample_pos = Arm()
AT (0, 0, 1.5) RELATIVE Origin

COMPONENT Sample=Isotropic_Sqw(
  radius_o = 0.01, yheight = 0.055,
  Sqw_coh=sample_coh, Sqw_inc=sample_inc, interpolate=interpolate
) AT (0, 0, 0) RELATIVE Sample_pos
EXTEND
%{
if (!SCATTERED) ABSORB;
%}

COMPONENT Radial = Collimator_radial(
    w1 = 0.017, h1 = 0.2, w2 = 0.034, h2 = 0.2, len = 0.2,
    divergence = 60*5, theta_min = -180, theta_max = 180,
    nchan = 72, radius = 0.2, roc = 1, verbose = 1)
  AT (0, 0, 0) RELATIVE Sample_pos

  /* theta - time detectors */

COMPONENT M_theta_t_all = Monitor_nD(
 xwidth=2.5, yheight=0.4,
 options="time limits=[1e-3 4e-3], theta limits=[-180 180], banana, parallel",
 bins=100)
AT (0,0,0) RELATIVE Sample_pos

END



