/*******************************************************************************
*         McStas instrument definition URL=http://www.mcstas.org
*
* Instrument: Test_Fermi
*
* %Identification
* Written by: E. Farhi [farhi@ill.fr]
* Date: Current Date
* Origin: ILL
* Release: McStas CVS-080208
* Version: $Revision: 1.1 $
* %INSTRUMENT_SITE:ILL
*
* Cross comparison of fermi Chopper components
*
* %Description
* Instrument longer description (type, elements, usage...)
*
* %Parameters
* Fermi: [1] Choice of Fermi chopper component to test, with
*            1=FermiChopper, 4=rotating Guide_gravity,
*            3=FermiChopper_ILL, 2=Vitess_ChopperFermi
*
*
* %End
*******************************************************************************/

/* Change name of instrument and input parameters with default values */
DEFINE INSTRUMENT test(Fermi=1)

TRACE

COMPONENT Origin = Progress_bar()
  AT (0,0,0) ABSOLUTE


COMPONENT Source = Source_gen(
    dist=1, xw = 0.05, yh = 0.05, Lambda0 = 3.39, dLambda = 0.3,
    h = 0.05, w = 0.05)
  AT (0, 0, 0) RELATIVE PREVIOUS
EXTEND %{
	t = randtriangle()*1e-3;	/* 1 ms triangle time window */
%}

COMPONENT Monitor1_xt = Monitor_nD(
    options = "multiple x y time, all auto", xwidth = 0.05, yheight = 0.05)
  AT (0, 0, 1) RELATIVE PREVIOUS

COMPONENT FC_Position = Arm( )
  AT (0, 0, 0.1) RELATIVE PREVIOUS

COMPONENT FC_Guide = Guide_gravity(
    w1 = 0.05, h1 = 0.05, w2 = 0.05, h2=0.05, l = 0.012, R0 = 0.0, k=120, d=0, nu=100)
  WHEN (Fermi == 4)
  AT (0, 0, -0.012/2.0) RELATIVE FC_Position
EXTEND %{
  if (!SCATTERED) ABSORB;
%}

COMPONENT FC_McStas = FermiChopper(
    radius = 0.1, nu = 100, width = 0.05, height = 0.05, Nslit=120, length=0.012)
  WHEN (Fermi == 1)
  AT (0, 0, 0) RELATIVE FC_Position

COMPONENT FC_ILL = FermiChopper_ILL(
    rad = 0.1, nu = 100, ymin = -0.05/2, ymax = 0.05/2,
    Nslit = 120, slit = 0.012, lambda = 3.39, width = 0.05)
  WHEN (Fermi ==3)
  AT (0, 0, 0) RELATIVE FC_Position

COMPONENT Fake_Origin = Arm()
  AT (0,0,0) ABSOLUTE

COMPONENT FC_Vitess = Vitess_ChopperFermi(
    sGeomFileName = "FC_geom_str.dat", Nchannels = 120, wallwidth=0,
    freq = 100, height = 0.05, width = 0.05, depth = 0.012,
    diameter = 0.1)
  WHEN (Fermi == 2)
  AT (0, 0, 1.1) RELATIVE PREVIOUS

COMPONENT Monitor2_xt = Monitor_nD(
    options = "multiple x y time limits=[0 3e-4]", xwidth = 0.05, yheight = 0.05)
  AT (0, 0, .1) RELATIVE FC_Position

END


