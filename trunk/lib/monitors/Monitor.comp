/***********************************************************************
*
* McStas, version 1.0, released October 26, 1998
*         Maintained by Kristian Nielsen and Kim Lefmann,
*         Risoe National Laboratory, Roskilde, Denmark
*
* Component: Monitor 
*
* Written by: KL, October 4, 1997
*
* Sums neutrons (0th, 1st, and 2nd moment of p) flying through 
* the rectangular monitor opening. May also be used as detector.
*
* INPUT PARAMETERS:
*
* xmin: Lower x bound of opening
* xmax: Upper x bound of opening
* ymin: Lower y bound of opening
* ymax: Upper y bound of opening
*
* OUTPUT PARAMETERS:
*
* Nsum:  Number of neutron hits
* psum:  Sum of neutron weights
* p2sum: 2nd moment of neutron weights
*
***********************************************************************/

DEFINE COMPONENT Monitor
DEFINITION PARAMETERS (xmin, xmax, ymin, ymax)
SETTING PARAMETERS ()
OUTPUT PARAMETERS (Nsum, psum, p2sum)
STATE PARAMETERS (x,y,z,vx,vy,vz,t,s1,s2,p)
DECLARE
  %{
    int Nsum;
    double psum, p2sum;
  %}
INITIALIZE
  %{
    psum = 0;
    p2sum = 0;
    Nsum = 0;
  %}
TRACE
  %{
    PROP_Z0;
    if (x>xmin && x<xmax && y>ymin && y<ymax)
    {
      Nsum++;
      psum += p;
      p2sum += p*p;
    }
  %}
FINALLY
  %{
    DETECTOR_OUT(Nsum, psum, p2sum);
  %}

MCDISPLAY
%{
  magnify("xy");
  multiline(5, (double)xmin, (double)ymin, 0.0,
	       (double)xmax, (double)ymin, 0.0,
	       (double)xmax, (double)ymax, 0.0,
	       (double)xmin, (double)ymax, 0.0,
	       (double)xmin, (double)ymin, 0.0);
%}

END
