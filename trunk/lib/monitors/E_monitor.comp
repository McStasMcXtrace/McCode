/***********************************************************************
*
* McStas, version 1.0, released October 26, 1998
*         Maintained by Kristian Nielsen and Kim Lefmann,
*         Risoe National Laboratory, Roskilde, Denmark
*
* Component: E_monitor
*
* Written by:  KN,KL,  April 20, 1998
* Modified by: KL, Octorber 7, 1998
*
* A square single monitor that measures the energy of the incoming neutrons.
*
* INPUT PARAMETERS:
*
* xmin:     Lower x bound of detector opening (m)
* xmax:     Upper x bound of detector opening (m)
* ymin:     Lower y bound of detector opening (m)
* ymax:     Upper y bound of detector opening (m)
* Emin:     Minimum energy to detect (meV)
* Emax:     Maximum energy to detect (meV)
* nchan:    Number of energy channels (1)
* filename: Name of file in which to store the detector image (text)
*
* OUTPUT PARAMETERS:
*
* E_N:      Array of neutron counts
* E_p:      Array of neutron weight counts
* E_p2:     Array of second moments
*
***********************************************************************/

DEFINE COMPONENT E_monitor
DEFINITION PARAMETERS (xmin, xmax, ymin, ymax, Emin, Emax, nchan, filename)
SETTING PARAMETERS ()
OUTPUT PARAMETERS (E_N, E_p, E_p2)
STATE PARAMETERS (x,y,z,vx,vy,vz,t,s1,s2,p)
DECLARE
  %{
    int E_N[nchan];
    double E_p[nchan], E_p2[nchan];
  %}
INITIALIZE
  %{
    int i;

    for (i=0; i<nchan; i++)
    {
      E_N[i] = 0;
      E_p[i] = 0;
      E_p2[i] = 0;
    }
  %}
TRACE
  %{
    int i;
    double E;

    PROP_Z0;
    if (x>xmin && x<xmax && y>ymin && y<ymax)
    {
      E = VS2E*(vx*vx + vy*vy + vz*vz);

      i = floor((E-Emin)*nchan/(Emax-Emin));
      if(i >= 0 && i < nchan)
      {
        E_N[i]++;
        E_p[i] += p;
        E_p2[i] += p*p;
      }
    }
  %}
FINALLY
  %{
    int i;
    FILE *outfile;

    outfile=fopen(filename,"w");
    if(outfile == NULL)
      fprintf(stderr, "Error: could not open output file '%s'\n", filename);
    else
    {
      for (i=0; i<nchan; i++)
        fprintf(outfile,"%g %d %g %g\n", 
                Emin + (double)i/nchan*(Emax-Emin), E_N[i], E_p[i], E_p2[i]);
      fclose(outfile);
    }
  %}
END
