DEFINE COMPONENT Res_monitor
DEFINITION PARAMETERS (xmin, xmax, ymin, ymax, filename)
SETTING PARAMETERS ()
OUTPUT PARAMETERS (Nsum, psum, p2sum, file)
STATE PARAMETERS (x,y,z,vx,vy,vz,t,s1,s2,p)
DECLARE
  %{
    int Nsum;
    double psum, p2sum;
    FILE *file;
  %}
INITIALIZE
  %{
    psum = 0;
    p2sum = 0;
    Nsum = 0;
    file = fopen(filename, "w");
    if(!file)
      fprintf(stderr, "Error: could not open output file '%s'\n", filename);
  %}
TRACE
  %{
    PROP_Z0;
    if (x>xmin && x<xmax && y>ymin && y<ymax)
    {
      Nsum++;
      psum += p;
      p2sum += p*p;
      /* Now fetch data from the Res_sample. */
      if(p != 0 && res_pi != 0 && file)
        fprintf(file, "%g %g %g %g %g %g %g %g %g %g %g\n",
                res_ki_x, res_ki_y, res_ki_z,
                res_kf_x, res_kf_y, res_kf_z,
                res_x, res_y, res_z, res_pi, p);
    }
  %}
FINALLY
  %{
    DETECTOR_OUT(Nsum, psum, p2sum);
    if(file)
      fclose(file);
  %}

MCDISPLAY
%{
  magnify("xy");
  multiline(5, (double)xmin, (double)ymin, 0.0,
               (double)xmax, (double)ymin, 0.0,
               (double)xmax, (double)ymax, 0.0,
               (double)xmin, (double)ymax, 0.0,
               (double)xmin, (double)ymin, 0.0);
%}

END
