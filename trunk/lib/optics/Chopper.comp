/*******************************************************************************
*
* McStas, the neutron ray-tracing package: Chopper.comp
*         Copyright 2000-2001 Risoe National Laboratory, Roskilde, Denmark
*
* Component: Chopper
*
* %I
* Written by: Philipp Bernhardt
* Date: Januar 22 1999
* Version: $Revision: 1.8 $
* Origin: McStas 1.5
*
* Disk chopper.
*
* %D
* Models a disc chopper with n identical slits, which are symmetrically disposed
* on the disc.
* If the chopper is the 1st chopper of the instrument, it sets t time with phase
*
* Example: Chopper(R=0.2, w=0.05, f=2500.0, n=3, pha=0, IsFirst=1) First chopper
*          Chopper(R=0.2, w=0.05, f=2500.0, n=3, pha=0, IsFirst=0)
*
* %P
* INPUT PARAMETERS:
*
* w:       (m)      Width of the slits at the bottom side
* R:       (m)      Radius of the disc
* f:       (rad/s)  angular frequency of the Chopper 
*                   (algebraic sign defines the direction of rotation)
* n:       (1)      Number of slits
* pha:     (s)      Phase
*
* Optional parameters:
* IsFirst  (0/1)    Set it to 1 for the first chopper position in a cw source
*                   (it then spreads the neutron time distribution)
*
* %D
* Example values: w=0.05 R=0.5 f=2500 n=3 pha=0
*
* %E
*******************************************************************************/
 
DEFINE COMPONENT Chopper
DEFINITION PARAMETERS ()
SETTING PARAMETERS (R, w=0.05, f=2500.0, n=3, pha=0, IsFirst=0)
OUTPUT PARAMETERS (Tg, To, IsFirst)
STATE PARAMETERS (x, y, z, vx, vy, vz, t, s1, s2, p)

DECLARE
 %{
      double Tg,To;
      char IsFirst;
 %}

INITIALIZE
 %{
      if (n<=0 || f<=0 || R <=0)
      { fprintf(stderr,"Chopper: %s: n,f and R must be > 0\n", NAME_CURRENT_COMP);
        exit(-1); }
      
      /* time between two pulses */
      Tg=2*PI/fabs(f)/n;

      /* how long can neutrons pass the Chopper at a single point */ 
      To=2*atan(w/R/2.0)/fabs(f);

 %}

TRACE
 %{
    double toff;

    PROP_Z0;
    
    if (IsFirst)
      t=atan2(x,y+R)/f+To*(rand01()-0.5)+pha+floor(a*rand01())*Tg; 
    else
    {
      toff=fabs(t-atan2(x,y+R)/f-pha)+To/2.0;

      /* does neutron hit the slit? */
      if (fmod(toff,Tg)>To) 
         ABSORB;
    }
    SCATTER;
 %}
 
MCDISPLAY
%{
  magnify("xy");
  circle("xy", 0, 0, 0, R);
%}
 
END
