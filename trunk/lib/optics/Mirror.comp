/*******************************************************************************
*
* McStas, version 1.0, released October 26, 1998
*         Maintained by Kristian Nielsen and Kim Lefmann,
*         Risoe National Laboratory, Roskilde, Denmark
*
* Component: Mirror
*
* Written by: KN, August 1998
* Modified by: KL, October 6, 1998
*
* Single mirror plate (used to build guides in compound components).
*
* INPUT PARAMETERS:
*
* xlenght: length of mirror plate (m)
* yheight: height of mirror plate (m)
* m:       m-value of material (1)
*
*******************************************************************************/

DEFINE COMPONENT Mirror
DEFINITION PARAMETERS (xlength, yheight,m)
SETTING PARAMETERS ()
STATE PARAMETERS (x,y,z,vx,vy,vz,t,s1,s2,p)
TRACE
%{
  double dt, q;
  
  /* First check if neutron has the right direction. */
  if(vz != 0.0 && (dt = -z/vz) >= 0)
  {
    double old_x = x, old_y = y;
    x += vx*dt;
    y += vy*dt;
    /* Now check if neutron intersects mirror. */
    if(x >= 0 && x <= xlength && y >= 0 && y <= yheight)
    {
      z = 0;
      t += dt;
      q = fabs(2*vz*MS2AA);
      vz = -vz;
      /* Reflectivity (see component Guide). */
      if(q > 0.02)
      {
        double arg = 300*(q-0.0219*m);
        if(arg < 10)
          p *= .5*(1-tanh(arg))*(1-6.49*(q-0.02));
        else
          ABSORB;                               /* Cutoff ~ 1E-10 */
      }
    }
    else
    {
      /* No intersection: restore neutron position. */
      x = old_x;
      y = old_y;
    }
  }
%}
END
