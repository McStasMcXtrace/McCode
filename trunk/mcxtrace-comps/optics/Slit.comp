/*******************************************************************************
*
* McXtrace, X-ray tracing package
*         Copyright 1997-2002, All rights reserved
*         Risoe National Laboratory, Roskilde, Denmark
*         Institut Laue Langevin, Grenoble, France
*
* Component: Slit
*
* %I
* Written by: Erik Knudsen
* Date: June 16, 2009
* Version: $Revision: 1.0$
* Origin: DTU Physics
* Release: McXtrace 0.1
*
* Rectangular/circular slit
*
* %D
* Based on Slit-comp by Kim Lefmann and Henrik Roennow
* A simple rectangular or circular slit. You may either
* specify the radius (circular shape), which takes precedence,
* or the rectangular bounds.
* No transmission around the slit is allowed.
* If cutting option is used, low-weight x-rays are ABSORBED
*
* Example: Slit(xmin=-0.01, xmax=0.01, ymin=-0.01, ymax=0.01)
*          Slit(radius=0.01, cut=1e-10)
*
* %P
* INPUT PARAMETERS:
* radius: Radius of slit in the z=0 plane, centered at Origin. (m)
* xmin: Lower x bound. (m)
* xmax: Upper x bound. (m)
* ymin: Lower y bound. (m)
* ymax: Upper y bound. (m)
* xwidth: Width of slit. Overrides xmin,xmax. (m)
* yheight: Height of slit. Overrides ymin,ymax. (m)
*
* Optional parameters:
* cut: Lower limit for allowed weight. (1)
* focus_xw: Width of resampling window. (m)
* focus_yh: Height of resampling window. (m)
* focus_x0: Centre (x) of resampling window. (m)
* focus_y0: Centre (y) of resampling window. (m)
* dist:     Distance from slit plane to plane containing resampling target. (m)
* %E
*******************************************************************************/


DEFINE COMPONENT Slit
DEFINITION PARAMETERS ()
SETTING PARAMETERS (xmin=-0.01, xmax=0.01, ymin=-0.01, ymax=0.01, radius=0, 
    cut=0, xwidth=0, yheight=0, dist=0,focus_xw=0, focus_yh=0, focus_x0=0, focus_y0=0)
OUTPUT PARAMETERS ( )
/* X-ray parameters: (x,y,z,kx,ky,kz,phi,t,Ex,Ey,Ez,p) */ 


INITIALIZE
%{
  if (xwidth)  { xmax=xwidth/2;  xmin=-xmax; }
  if (yheight) { ymax=yheight/2; ymin=-ymax; }
  if (xmin == 0 && xmax == 0 && ymin == 0 && ymax == 0 && radius == 0)
    { fprintf(stderr,"Slit: %s: Error: give geometry\n", NAME_CURRENT_COMP); exit(-1); }

  if ( (focus_xw || focus_yh || dist) && !( focus_xw && focus_yh && dist) ){
    fprintf(stderr,"Error (%s): Inconsistent target definition\n",NAME_CURRENT_COMP);
    exit(-1);
  }

%}

TRACE
%{
    PROP_Z0;
    if (((radius == 0) && (x<xmin || x>xmax || y<ymin || y>ymax))
    || ((radius != 0) && (x*x + y*y > radius*radius))){
      ABSORB;
    }else{
      if (p < cut)
        ABSORB;
      else{
        SCATTER;
        if ( focus_xw ){
          double posx,posy,posz,pdir,k;
          coords_get(POS_A_CURRENT_COMP,&posx,&posy,&posz);

          /*we have a target behind the slit - so we now consider the ray a Huygens wavelet.*/
          double xf,yf,zf;
          randvec_target_rect_real(&xf, &yf, &zf, &pdir,
              focus_x0-posx,focus_y0-posy,dist, focus_xw, focus_yh, ROT_A_CURRENT_COMP, x, y, z, 0);
          //printf("%g %g %g %g %g    %g %g %g   %g %g\n",xf,yf,zf,pdir,p,x,y,z,focus_x0,focus_y0);
          //p*=pdir;
          k=sqrt(scalar_prod(kx,ky,kz,kx,ky,kz));
          kx=(xf-x); ky=(yf-y); kz=(zf-z);
          NORM(kx,ky,kz);
          kx*=k;ky*=k;kz*=k;
        }
      }

    }
%}


MCDISPLAY
%{
  magnify("xy");
  if (radius == 0) {
    double xw, yh;
    xw = (xmax - xmin)/2.0;
    yh = (ymax - ymin)/2.0;
    multiline(3, xmin-xw, (double)ymax, 0.0,
              (double)xmin, (double)ymax, 0.0,
              (double)xmin, ymax+yh, 0.0);
    multiline(3, xmax+xw, (double)ymax, 0.0,
              (double)xmax, (double)ymax, 0.0,
              (double)xmax, ymax+yh, 0.0);
    multiline(3, xmin-xw, (double)ymin, 0.0,
              (double)xmin, (double)ymin, 0.0,
              (double)xmin, ymin-yh, 0.0);
    multiline(3, xmax+xw, (double)ymin, 0.0,
              (double)xmax, (double)ymin, 0.0,
              (double)xmax, ymin-yh, 0.0);
  } else {
    circle("xy",0,0,0,radius);
  }
%}

END
