/*******************************************************************************
*
* McXtrace, xray-tracing package
*         Copyright 1997-2016, All rights reserved
*         DTU Physics, Kgs. Lyngby, Denmark
*         Institut Laue Langevin, Grenoble, France
*
* Component: Divergence_monitor
*
* %I
* Written by: Erik B Knudsen
* Based on neutron component by Kim Lefmann
* Date: Jun. '16
* Origin: DTU Physics
* Release: McXtrace 1.3
*
* Horizontal+vertical divergence monitor.
*
* %D
* A divergence sensitive monitor. The counts are distributed in
* (n times m) pixels.
*
* Example: Divergence_monitor(nh=20, nv=20, filename="Output.pos",
*           xmin=-0.1, xmax=0.1, ymin=-0.1, ymax=0.1,
*           maxdiv_h=2, maxdiv_v=2)
*
* %P
* INPUT PARAMETERS:
*
* xmin:     Lower x bound of detector opening (m)
* xmax:     Upper x bound of detector opening (m)
* ymin:     Lower y bound of detector opening (m)
* ymax:     Upper y bound of detector opening (m)
* xwidth:   Width of detector. Overrides xmin,xmax. (m)
* yheight:  Height of detector. Overrides ymin,ymax. (m)
* nv:       Number of pixel columns (1)
* nh:       Number of pixel rows (1)
* nx:       (1)
* ny:       Vector definition of "forward" direction wrt. divergence, to be used e.g. when the monitor is rotated into the horizontal plane.(1)
* nz:       (1)
* maxdiv_v: Maximal vertical divergence detected (degrees/rad)
* maxdiv_h: Maximal horizontal divergence detected (degrees/rad)
* filename: Name of file in which to store the detector image (text)
* restore_xray: If set, the monitor does not influence the xray state (1)
* rad:   If set - divergence will be measured in radians. (1)
*
* OUTPUT PARAMETERS:
*
* Div_N:    Array of photon ray counts
* Div_p:    Array of photon weight counts
* Div_p2:   Array of second moments
*
* %E
*******************************************************************************/
DEFINE COMPONENT Divergence_monitor
DEFINITION PARAMETERS (nh=20, nv=20, rad=0)
SETTING PARAMETERS (string filename=0, xmin=-0.05, xmax=0.05, ymin=-0.05, ymax=0.05, 
		    xwidth=0, yheight=0, maxdiv_h=1, maxdiv_v=1, restore_xray=0, nx=0, ny=0, nz=1)
OUTPUT PARAMETERS (Div_N, Div_p, Div_p2)
    /* Xray  parameters: (x,y,z,kx,ky,kz,phi,t,Ex,Ey,Ez,p) */ 
DECLARE
  %{
    double Div_N[nh][nv];
    double Div_p[nh][nv];
    double Div_p2[nh][nv];
  %}
INITIALIZE
  %{
    int i,j;

    if (xwidth  > 0) { xmax = xwidth/2;  xmin = -xmax; }
    if (yheight > 0) { ymax = yheight/2; ymin = -ymax; }

    if ((xmin >= xmax) || (ymin >= ymax)) {
            printf("Divergence_monitor: %s: Null detection area !\n"
                   "ERROR               (xwidth,yheight,xmin,xmax,ymin,ymax). Exiting",
           NAME_CURRENT_COMP);
      exit(0);
    }

    for (i=0; i<nh; i++)
     for (j=0; j<nv; j++)
     {
      Div_N[i][j] = 0;
      Div_p[i][j] = 0;
      Div_p2[i][j] = 0;
     }
    NORM(nx,ny,nz);
  %}
TRACE
  %{
    int i,j;
    double h_div, v_div;
    double k, kn;

    PROP_Z0;
    if (x>xmin && x<xmax && y>ymin && y<ymax)
    {
      /* Find length of projection onto the [nx ny nz] axis */
      kn = scalar_prod(kx, ky, kz, nx, ny, nz);
      if (rad){
          h_div = atan2(kx,kn);
          v_div = atan2(ky,kn);
      }else{
          h_div = RAD2DEG*atan2(kx,kn);
          v_div = RAD2DEG*atan2(ky,kn);
      }
      if (h_div < maxdiv_h && h_div > -maxdiv_h &&
          v_div < maxdiv_v && v_div > -maxdiv_v)
      {
        i = floor((h_div + maxdiv_h)*nh/(2.0*maxdiv_h));
        j = floor((v_div + maxdiv_v)*nv/(2.0*maxdiv_v));
        Div_N[i][j]++;
        Div_p[i][j] += p;
        Div_p2[i][j] += p*p;
        SCATTER;
      }
    }   
    if (restore_xray) {
        RESTORE_XRAY(INDEX_CURRENT_COMP, x, y, z, kx, ky, kz, phi, t, Ex, Ey, Ez, p);
    }
  %}
SAVE
  %{
      if (rad){
          DETECTOR_OUT_2D(
                  "Divergence monitor",
                  "X divergence [rad]",
                  "Y divergence [rad]",
                  -maxdiv_h, maxdiv_h, -maxdiv_v, maxdiv_v,
                  nh, nv,
                  &Div_N[0][0],&Div_p[0][0],&Div_p2[0][0],
                  filename);
      } else {
          DETECTOR_OUT_2D(
                  "Divergence monitor",
                  "X divergence [deg]",
                  "Y divergence [deg]",
                  -maxdiv_h, maxdiv_h, -maxdiv_v, maxdiv_v,
                  nh, nv,
                  &Div_N[0][0],&Div_p[0][0],&Div_p2[0][0],
                  filename);
      }
  %}

MCDISPLAY
  %{
    
    multiline(5, (double)xmin, (double)ymin, 0.0,
                 (double)xmax, (double)ymin, 0.0,
                 (double)xmax, (double)ymax, 0.0,
                 (double)xmin, (double)ymax, 0.0,
                 (double)xmin, (double)ymin, 0.0);
%}

END
