/*******************************************************************************
* Instrument: Template_DCM
*
* %I
* Written by: Erik B Knudsen <erkn@fysik.dtu.dk>
* Date: Jan '21
* Origin: DTU Physics
* Version: 1.0
* %INSTRUMENT_SITE: Tests_optics
*
* Template for a vertically defleting DCM
*
* %D
* Simple skeleton instrument showing a vertically deflecting
* double crystal monochromator. To instead make it horizontally deflecting
* simply add Arms before and after the crystal assembly that rotate by 
* 90 and -90 deg. respectively around the z-axis.
* The crystal is illuminated by a model point source.
*
* %Example: theta=9.17 alpha=0.01 Detector: emon_dcm1_I=8.83566e-21
*
* %P
* E0:    [keV] Central energy to be emitted by the source. 0=set from Bragg's law.
* dEr:   [keV] Relative half width to be emitted by the source
* theta: [deg] Rotation angle of the crystals. 0=set from Bragg's law.
* alpha: [deg] Asymmetry angle for the crystals. 0=symmetric
* 
* %E
*******************************************************************************/
DEFINE INSTRUMENT Template_DCM(E0=12.3984, dEr=2e-2, theta=9.18,alpha=0)

DECLARE
%{
  double dcm_gap;
  double theta;
  double dE;
%}

INITIALIZE
%{
   double DM= 5.4909;     // Si d-spacing
   double d = DM/sqrt(3); // <111> reflection |<111>|=3
   dcm_gap  = 0.02;       // gap between the 2 monochromator crystals
   
   if (!theta && E0) {
     // n.lambda = 2 d sin(theta) = 2*PI/E2K / E0 with n=ORDER=1
     double sin_theta = 2*PI/E2K/E0 / 2 / d;
     if (fabs(sin_theta) < 1)
       theta = asin(sin_theta)*RAD2DEG;
   } else if (theta && !E0)
     E0 = 2*PI/E2K / (2*d*sin(theta*DEG2RAD));
   dE      = dEr*E0;
   if (!theta || !E0)
     exit(fprintf(stderr, "%s: ERROR: Monochromator can not reflect E0=%g [keV]. Aborting.\n", __FILE__, E0));
   printf("%s: Monochromator theta=%g [deg] E0=%g [keV]\n", __FILE__, theta, E0);
%}

TRACE

REMOVABLE COMPONENT origin = Progress_bar()
AT (0, 0, 0) RELATIVE ABSOLUTE

REMOVABLE COMPONENT source_pt = Source_pt(
    focus_xw=2e-3, 
    focus_yh=0.5e-3, 
    E0=E0, 
    dE=dE)
AT (0, 0, 0) RELATIVE PREVIOUS

COMPONENT emon_dcm0 = E_monitor(xwidth=0.05, yheight=0.05, filename="emon_dcm0", Emin=E0-dE*2, Emax=E0+dE*2, nE=511)
AT(0,0,1) RELATIVE PREVIOUS

COMPONENT dcm_xtal0 = Bragg_crystal(
    length=0.04, width=0.04, 
    alpha=alpha, h=1, k=1, l=1, material="Si.txt", V=160.1826)
AT(0,0,0.02) RELATIVE PREVIOUS
ROTATED (-theta,0,0) RELATIVE PREVIOUS

COMPONENT dcm0 = Arm()
AT(0,0,0) RELATIVE dcm_xtal0
ROTATED (-theta,0,0) RELATIVE PREVIOUS

COMPONENT dcm_xtal1 = COPY(dcm_xtal0)
AT(0,0,dcm_gap) RELATIVE dcm0
ROTATED (theta,0,0) RELATIVE dcm0

COMPONENT dcm1 =Arm()
AT(0,0,0) RELATIVE dcm_xtal1
ROTATED (theta,0,0) RELATIVE dcm_xtal1 

COMPONENT emon_dcm1 = COPY(emon_dcm0)(filename="emon_dcm1")
AT(0,0,0.02) RELATIVE dcm1 

FINALLY
%{
%}

END
