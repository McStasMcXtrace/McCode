/*******************************************************************************
*
* McXtrace, X-ray tracing package
*         Copyright, All rights reserved
*         Risoe National Laboratory, Roskilde, Denmark
*         University of Copenhagen, Denmark
*         Institut Laue Langevin, Grenoble, France
*
* Component: Source_div
*
* %I
* Written by: Erik Knudsen 
* Date: November 11, 2009
* Version: $Revision$
* Origin: Risoe
*
* X-ray source with Gaussian or uniform divergence
*
* %D
* A flat rectangular surface source with uniform or Gaussian divergence profile and focussing.
* If the parametere gauss is not set (the default) the divergence profile is flat
* in the range [-focus_ax,focus_ay]. If gauss is set, the focux_ax,focus_ay is considered
* the standard deviation of the gaussian profile.
* Currently focussing is only active for flat profile. The "focus window" is defined by focus_xw,focus_yh and dist.
* The spectral intensity profile is uniformly distributed in the energy interval defined by e0+-dE/2 or 
* by wavelength lambda0+-dlambda/2
* 
* Example: Source_div(xwidth=0.1, yheight=0.1, focus_aw=2, focus_ah=2, E0=14, dE=2, gauss=0)
*
* %VALIDATION
*
* %BUGS
*
* %P
* xwidth:   [m]  Width of source.
* yheight:  [m]  Height of source.
* focus_aw:[rd] Standard deviation (Gaussian) or maximal (uniform) horz. width divergence.
* focus_ah:[rd] Standard deviation (Gaussian) or maximal (uniform) vert. height divergence.
* E0:      [keV] Mean energy of X-rays.
* dE:      [keV] Energy half spread of X-rays. If gauss==0 dE is the half-spread, i.e. E\in[E0-dE,E0+dE], if gauss!=0 it's interpreted as the standard dev. 
* lambda0: [AA] Mean wavelength of X-rays (only relevant for E0=0).
* dlambda: [AA] Wavelength half spread of X-rays.
* gauss:  [1]  Criterion: 0: uniform, 1: Gaussian distribution of energy/wavelength.
* gauss_a:   [1] Criterion: 0: uniform, 1: Gaussian divergence distribution.
* flux:   [1/(s * mm**2 *mrad**2 * energy unit]) flux per energy unit, Angs or keV.
* randomphase [1] If !=0 the photon phase is chosen randomly.
* phase [1] Value of the photon phase (if randomphase==0).
* spectrum_file: [ ] File from which to read the spectral intensity profile
*
* OUTPUT PARAMETERS:
*
* %E
*******************************************************************************/

DEFINE COMPONENT Source_div
DEFINITION PARAMETERS (string spectrum_file=NULL)
SETTING PARAMETERS (xwidth=0, yheight=0, focus_aw=0, focus_ah=0, focus_ar=0,radius=0,
    E0=0, dE=0, lambda0=0, dlambda=0, flux=0, gauss=0, gauss_a=0, randomphase=1, phase=0)
OUTPUT PARAMETERS (prms, p_init, dist)
/* X-ray parameters: (x,y,z,kx,ky,kz,phi,t,Ex,Ey,Ez,p) */ 

SHARE
%{
  %include "read_table-lib"
%}

DECLARE
%{
  double  p_init, dist;
  struct {
    double K,dK,xmin,xmax,xw_2,focus_xw_2,ymin,ymax,yh_2,focus_yh_2,pmul,pint;
    t_Table T;
  } prms;
%}

INITIALIZE
%{

  prms.xmin=-xwidth/2.0;
  prms.ymin=-yheight/2.0;
  prms.xmax=xwidth/2.0;
  prms.ymax=yheight/2.0;

  if(radius==0 && ( prms.xmax==prms.xmin && prms.ymin==prms.xmax) ){
    printf("ERROR(%s): No meaningful source area set. Either set radius or xwidth and yheight.\n",NAME_CURRENT_COMP);
  }

  if (spectrum_file){
    /*read spectrum from file*/
    int status=0;
    if ( (status=Table_Read(&(prms.T),spectrum_file,0))==-1){
      fprintf(stderr,"Source_div(%s) Error: Could not parse file \"%s\"\n",NAME_CURRENT_COMP,spectrum_file?spectrum_file:"");
      exit(-1);
    }
    /*data is now in table t*/
    /*integrate to get total flux, assuming raw numbers have been corrected for measuring aperture*/
    int i;
    prms.pint=0;
    t_Table *T=&(prms.T);
    for (i=0;i<prms.T.rows-1;i++){
      prms.pint+=((T->data[i*T->columns+1]+T->data[(i+1)*T->columns+1])/2.0)*(T->data[(i+1)*T->columns]-T->data[i*T->columns]); 
    }
    printf("INFO: Source_div(%s): Integrated intensity radiated is %g pht/s\n",NAME_CURRENT_COMP,prms.pint);
    if(E0) printf("INFO: Source_div(%s): E0!=0 -> assuming intensity spectrum is parametrized by energy [keV]\n",NAME_CURRENT_COMP);
  }else if (!E0 && !lambda0){
    fprintf(stderr,"ERROR (%s): Error: Must specify either wavelength or energy distribution\n",NAME_CURRENT_COMP);
    exit(-1);
  }  

  /*calculate the X-ray weight from the flux*/
  if (flux){
    prms.pmul=flux;
  }else{
    prms.pmul=1;
  }
  prms.pmul*=1.0/((double) mcget_ncount());


%}

TRACE
%{
  double kk,theta_x,theta_y,l,e,k;
  double tan_thx, tan_thy;

  p=prms.pmul;

  if (spectrum_file){
    double pp=0;
    while (pp<=0){ 
      l=prms.T.data[0]+ (prms.T.data[(prms.T.rows-1)*prms.T.columns] -prms.T.data[0])*rand01();
      pp=Table_Value(prms.T,l,1);
    }
    p*=pp;
    /*if E0!=0 the tabled value is assumed to energy in keV*/
    if (E0!=0){
      k=E2K*l;
    }else{
      k=(2*M_PI/l);
    }
  }else if (E0){
    if(!dE){
      e=E0;
    }else if (gauss){
      e=E0+dE*randnorm();
    }else{
      e=randpm1()*dE + E0;
    }
    k=E2K*e;
  }else if (lambda0){
    if (!dlambda){
      l=lambda0;
    }else if (gauss){
      l=lambda0+dlambda*randnorm();
    }else{
      l=randpm1()*dlambda*0.5 + lambda0;
    }
    k=(2*M_PI/l);
  }

  if(!radius){
    x=prms.xmin+rand01()*xwidth;
    y=prms.ymin+rand01()*yheight;
    z=0;
  }else{
    double r=radius*sqrt(rand01());
    double th=rand01()*2*M_PI;
    x=r*cos(th);
    y=r*sin(th);
    z=0;
  }

  if (focus_aw!=0 || focus_ah!=0){
    if (!gauss_a){
      /*find limits of uniform sampling scheme for vertical divergence.
        thetav should be acos(1-2*U) for U\in[0,1]. for theta measured from vertical axis
        we only use a sub-interval for U and measure from horizontal plane.*/
      double sample_lim1,u2;
      sample_lim1=(1-cos(M_PI_2 - focus_ah/2.0))*0.5;
      u2=randpm1()*(sample_lim1-0.5) + 0.5;
      theta_x = randpm1()*focus_aw/2.0;
      theta_y = acos(1-2*u2) - M_PI_2;
      //theta_x=(rand01()-0.5)*focus_aw;
      //theta_y=(rand01()-0.5)*focus_ah;
    }else{
      theta_x=randnorm()*focus_aw;
      theta_y=randnorm()*focus_ah;
    }

    tan_thx=tan(theta_x);
    tan_thy=tan(theta_y);

    kz=k / sqrt(1 + tan_thx*tan_thx + tan_thy*tan_thy);
    kx=tan_thx*k;
    ky=tan_thy*k;
  }else{
    /*radial divergence profile*/
    double theta,phi,kp;
    if(!gauss_a){
      double sample_lim1,u2;
      sample_lim1=(1-cos(focus_ar/2.0))*0.5;
      u2=rand01()*(sample_lim1);
      phi=acos(1-2*u2);
    }else{
      do{
        phi=abs(randnorm()*focus_ar);
      } while (phi>M_PI);
    }
    theta=rand01()*2*M_PI;

    kz=k*cos(phi);
    kp=sqrt(k*k-kz*kz);
    kx=kp*sin(theta);
    ky=kp*cos(theta);
  }

  /*set polarization and phase.*/
  Ex=0;Ey=0;Ez=0;
  if (!randomphase){
    phi=phase;
  }else{
    phi=rand01()*M_2_PI;
  }

  /*set polarization vector*/
  Ex=0;Ey=0;Ez=0;

%}

MCDISPLAY
%{
  
  double dist=1;
  multiline(5, -xwidth/2.0, -yheight/2.0, 0.0,
                xwidth/2.0, -yheight/2.0, 0.0,
                xwidth/2.0,  yheight/2.0, 0.0,
               -xwidth/2.0,  yheight/2.0, 0.0,
               -xwidth/2.0, -yheight/2.0, 0.0);
  if (focus_aw){
    dashed_line(0,0,0, tan(focus_aw/2.0)*dist,0,dist,4);
    dashed_line(0,0,0,-tan(focus_aw/2.0)*dist,0,dist,4);
  }
  if (focus_ah){
    dashed_line(0,0,0,0, tan(focus_ah/2.0)*dist,dist,4);
    dashed_line(0,0,0,0,-tan(focus_ah/2.0)*dist,dist,4);
  }
%}

END
