/************************************************************************
* 
* McXtrace X-ray tracing software
* Copyright, All Rights Reserved
* Risoe-DTU, Roskilde, Denmark
*
*
* Component: Bending_magnet
*
* %I
* Written by: Erik Knudsen
* Date: May, 2013.
* Version: 1.0
* Origin: DTU Physics
*
* Model of a bending magnet source
* 
* %D
* A source model based on the derivation from B.D. Patterson, Am. J. Phys. 79, 1046 (2011); doi: 10.1119/1.3614033
*
* %P
* Input Parameters:
* gamma: [ ] Gamma parameter of the Bending Magnet
* Ee: [GeV] Storage ring electron energy (typically a few GeV)
* Ie: [A]   Ring current
* tbunch: [s] Bunch length. 0 means continous source.
* t0:     [s] Delay of the first bunch from t=0
* B:      [T] Magnet field strength
* sigex:  [ ] Electron ring beam size in horizontal plane (rms) 
* sigey:  [ ] Electron ring beam size in vertical plane (rms)
* phase:  [ ] Initial phase of radiation.
* randomphase: [ ] If !=0 phase will be random (I.e. the emitted radiation is completely incoherent)
* focus_xw: [m] Width of target window
* focus yh: [m] Height of traget window
* dist: [m] Distance from source plane to target window along the optical axis
* gauss_t: [ ] if 0 the target window will be sampled uniformly and the weight adjusted accordingly, otherwise we will use a gaussian sampling scheme.
* E0: [keV] Center of emitted energy spectrum (overrides lambda0)
* dE: [keV] Half-width of emitted energy spectrum
* lambda0: [AA] Center of emitted wavelength spectrum
* dlambda: [AA] Half-width of emitted wavelength spectrum
* 
* %E
**********************/

DEFINE COMPONENT Bending_magnet
DEFINITION PARAMETERS ()
SETTING PARAMETERS (E0=0, dE=0, lambda0=0,dlambda=-1, phase=0, randomphase=1, Ee=2.4, Ie=0.4, tbunch=0, t0=0, B=1.6, sigey=0, sigex=0, focus_xw=0, focus_yh=0, dist=1, gauss_t=0)
OUTPUT PARAMETERS (prms,alpha,MELE)
/* X-ray parameters: (x,y,z,kx,ky,kz,phi,t,Ex,Ey,Ez,p) */ 

SHARE
%{
//  %include "read_table-lib"

#ifndef MCCODE_BESSELKNU
#define MCCODE_BESSELKNU 1

  double besselKnu(double nu, double x){
    const double h=0.5;
    double KK=0,dK;
    int r=0;
    const int maxiter=1000;
    KK=exp(-x)/2.0;
    dK=1;
    while (dK>DBL_EPSILON && r<maxiter){
      r++;
      dK=exp(-x*cosh(r*h))*cosh(nu*r*h);
      KK+=dK;
    }
    if (r>=maxiter) {
      fprintf(stderr,"Warning (%s): Maximum number of iterations exceeded in besselKnu(%g,%g).\n",NAME_CURRENT_COMP,nu,x);
    }
    KK*=h;
    return KK;
  }
#endif /*MCCODE_BESSELKNU*/
%}

DECLARE
%{
  struct {
    double gamma,gamma2,igamma;
    double kc; /*characteristic wavenumber of radiation from bending magnet*/
    double s1x,s1y; /*beam's size at dist (convolution of sigex/sigey and igamma)*/
  } prms;
  /*fine structure constant from CODATA*/
  const double alpha=7.2973525698e-3;
  const double MELE=9.10938291e-31;
  double p0;
%}


INITIALIZE
%{

  fprintf(stderr,"Warning (%s): Bending_magnet is an experimental component - testing is ongoing\n",NAME_CURRENT_COMP);

  if(B<=0 || Ee<=0 || Ie<=0 ){
    fprintf(stderr, "Error (%s): B, Ee, and Ie must all be >= 0. Found (%g %g %g). Aborting.\n",NAME_CURRENT_COMP,B,Ee,Ie);
    exit(1);
  }

  if (sigex <0 || sigey<0){
    fprintf(stderr, "Error (%s): sigex and sigey must be > 0. Negative beam size isn't meaningful. Aborting.\n",NAME_CURRENT_COMP);
    exit(1);
  }
  if (dist<=0){
    fprintf(stderr,"Error (%s): Target undefined.\n",NAME_CURRENT_COMP);
    exit(1);
  }

  /*compute gamma*/
  prms.gamma=(Ee*1e9)/(MELE/CELE*M_C*M_C);/*the extra CELE is to convert to eV*/
  prms.gamma2=prms.gamma*prms.gamma;
  prms.igamma=1.0/prms.gamma;

  //printf("Bending_magnet (%s): gamma=%g, divergence is 1/gamma=%g rad.\n",NAME_CURRENT_COMP,prms.gamma,prms.igamma);
  /*compute characteristic energy in keV*/
  double Ec=0.665*Ee*Ee*B;
  //double Ec=1.5*prms.gamma2*HBAR*CELE*B/MELE *1e-3; /*check units on this one. The 1e-3 factor is because energy is assumed to be in keV*/
  /*We normally do computations in k so use that for transfer*/
  prms.kc=E2K*Ec;

  prms.s1x=sqrt(sigex*sigex + prms.igamma*prms.igamma*dist*dist);
  prms.s1y=sqrt(sigey*sigey + prms.igamma*prms.igamma*dist*dist);
  p0=1.0/mcget_ncount();
%}


TRACE
%{

  double xx,yy,x1,y1,z1;
  double k,e,l;
  double F1=1.0;
  double dx,dy,dz;
  
  // initial source area
  xx=randnorm();
  yy=randnorm();
  x=xx*sigex;
  y=yy*sigey;
  z=0;

  // Gaussian distribution at origin
  p=p0;/*initial weight is p0*/
  if (E0){
    if(!dE){
      e=E0;
    }else {
      e=randpm1()*dE + E0;
    }
    k=E2K*e;
  }else if (lambda0){
    if (!dlambda){
      l=lambda0;
    }else{
      l=randpm1()*dlambda + lambda0;
    }
    k=(2*M_PI/l);
  }
  // targeted area calculation
  if (focus_xw){
    if (!gauss_t){
      /*sample uniformly but adjust weight*/
      x1=randpm1()*focus_xw/2.0;
      p*=exp(-(x1*x1)/(2.0*prms.s1x*prms.s1x));
    }else {
      do {
        x1=randnorm()*prms.s1x;
      }while (focus_xw!=0 && fabs(x1)>focus_xw/2.0);
      /*adjust for restricted sampling window*/
      p*=erf(focus_xw*0.5*M_SQRT1_2/prms.s1x);
    }
  }else{
    x1=randnorm()*prms.igamma;
  }
  if (focus_yh){
    if (!gauss_t){
      /*sample uniformly but adjust weight*/
      y1=randpm1()*focus_yh/2.0;
      p*=exp(-(y1*y1)/(2.0*prms.s1y*prms.s1y));
    }else {
      do {
        y1=randnorm()*prms.s1y;
      }while (fabs(y1)>focus_yh/2.0);
      /*adjust for restricted sampling window*/
      p*=erf(focus_yh*0.5*M_SQRT1_2/prms.s1y);
    }
  }else{
    y1=randnorm()*prms.igamma;
  }
  z1=dist;
  dx=x1-x;
  dy=y1-y;
  dz=sqrt(dx*dx+dy*dy+dist*dist);

  kx=(k*dx)/dz;
  ky=(k*dy)/dz;
  kz=(k*dist)/dz;
  
  /*spectral strength of radiation is given by Patterson*/  
  double k_kc=k/prms.kc;
  double K2_3=besselKnu(0.666666666666666666666666667,k_kc*0.5);
  p*=1.33e13*Ee*Ee*Ie* k_kc*k_kc*K2_3*K2_3;
  //p*=alpha/(M_PI*M_PI)*prms.gamma2*Ie/CELE* 1e-4 * 0.75 *k_kc*k_kc*K2_3*K2_3; 
  

  /*randomly pick phase*/
  if (randomphase){
    phi=rand01()*2*M_PI;
  }else{
    phi=phase;
  }

  /*set polarization vector*/
  Ex=0;Ey=0;Ez=0;
%}

MCDISPLAY
%{
  

  double radius,D;
  radius=3.3*Ee/B;;
  D=0.1;
  double x0,x1,z0,z1;
  const double phimin=-2.0*DEG2RAD, phimax=2.0*DEG2RAD;
  double phi=phimin,dphi;  
  dphi=(phimax-phimin)/32;
  while (phi<phimax){
    x0=radius*(1-cos(phi));
    x1=radius*(1-cos(phi+dphi));
    z0=radius*sin(phi);
    z1=radius*sin(phi+dphi);
    line(x0,0.0,z0,x1,0.0,z1);
    phi+=dphi;
  }

  line(0.0,0.0,0.0, D*sin(prms.igamma), 0.0, D);
  line(0.0,0.0,0.0,-D*sin(prms.igamma), 0.0, D);
  line(0.0,0.0,0.0, 0.0, D*sin(prms.igamma), D);
  line(0.0,0.0,0.0, 0.0,-D*sin(prms.igamma), D);
  
  phi =-prms.igamma;
  dphi= 2.0*prms.igamma/32;
  while(phi<prms.igamma){
    x0=D*sin(phi);
    x1=D*sin(phi+dphi);
    z0=D*cos(phi);
    z1=D*cos(phi+dphi);
    line(x0,0.0,z0,x1,0.0,z1);
    line(0.0,x0,z0,0.0,x1,z1);
    phi+=dphi;
  }
%}

END
