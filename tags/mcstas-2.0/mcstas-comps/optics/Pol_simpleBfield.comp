/**************************************************************************
*
* McStas, neutron ray-tracing package
*         Copyright 1997-2006, All rights reserved
*         Risoe National Laboratory, Roskilde, Denmark
*         Institut Laue Langevin, Grenoble, France
*
* Component: Pol_simpleBfield
*
* %I
* Written by: Erik B Knudsen, Peter Christiansen and Peter Willendrup
* Date: July 2011
* Version: $Revision: 1.1 $
* Origin: RISOE
* Release: McStas 1.12
*
* Magnetic field component.
*
* %D 
*
* Region with a definable magnetic field.
*
* The component is nestable. This means that it requires a
*
* // START MAGNETIC FIELD
* COMPONENT msf =
* Pol_simpleBfield(xwidth=0.08, yheight=0.08, zdepth=0.2, Bx=0, By=-0.678332e-4, Bz=0)
*      AT (0, 0, 0) RELATIVE armMSF
*
* // HERE CAN BE OTHER COMPONENTS INSIDE THE MAGNETIC FIELD
*
* // STOP MAGNETIC FIELD
* COMPONENT msf_stop = Pol_simpleBfield_stop(magnet_comp_stop=msf)
*      AT (0, 0, 0) RELATIVE armMSF
*
* In between the two component instances the propagation routine
* PROP_DT also handles spin propagation.
* The current algorithm used for spin propagation is:
* SimpleNumMagnetPrecession
* in pol-lib.
*
* Example: Pol_simpleBfield(xwidth=0.1, yheight=0.1, zdepth=0.2, Bx=0, By=1, Bz=0)
*
* The magetic field may be given as any definable c-function with a specific footprint:
* int func(double x, double y, double z, double t, double *bx, double *by, double *bz, void *data)
*  where the magnetic field at coordinates (x,y,z, t) is returned in the variables pointed to by
*  bx,by, and bz. Additional parameters to be handed to the function should be handed to the function
*  using the pointer data.
*
* Functions supplied by the system are
* 1. const_magnetic_field: Constant field (Bx,By,Bz) within the region
* 2. rot_magnetic_field: Field is initially (0,By,0) but after a length of zdepth has rotated to (By,0,0)
* 3. majorana_magnetic_field: Field is initially (Bx,By,0) liearly transforms to (-Bx,By,0) after zdepth
*    where By<<Bx
* 4. table_magnetic_field: The magnetic field is read from a file where it is specified as a point cloud
*    Assuming N is the number of points in the cloud, the file format is;
*      N
*      x y z Bx By Bz
* Users may specify any other function with the required footprint to have any other field. Note that coordinates refer
* the coordinate system defined by this component.
*
* The magnetic field is turned on by an instance of this component, and turned off by an instance of Pol_simpleBfield_stop
* Anything in between is considered inside the field.
*
* %P
* INPUT PARAMETERS:
*
* xwidth:      [m]       Width of opening. 
* yheight:      [m]       Height of opening. 
* zdepth:  [m]       Length of field.
* Bx:      [T]       Parameter used for x composant of field.
* By:      [T]       Parameter used for y composant of field.
* Bz:      [T]       Parameter used for z composant of field.
* fieldFunction [pointer] Function describing the magnetic field.
* filename []        file where the magnetic field is tabulated.
*
* OUTPUT PARAMETERS:
*
* gz:             z-component of gravity (m/s/s)
* parPtr:         Pointer to component magnet parameters (pointer)
*
* %E
****************************************************************************/

DEFINE COMPONENT Pol_simpleBfield
DEFINITION PARAMETERS (fieldFunction=const_magnetic_field)
SETTING PARAMETERS (xwidth, yheight,zdepth,
		       Bx, By, Bz,
                       string filename="bfield.dat")
OUTPUT PARAMETERS(gz, magnet, parPtr, vertices, vertices2, tree)
/* Neutron parameters: (x,y,z,vx,vy,vz,t,sx,sy,sz,p) */ 

SHARE
%{
  %include "pol-lib"
  double fmax(double, double);
  double fmin(double, double);

  int table_magnetic_field(double x, double y, double z, double t, double *bx, double *by, double *bz, void *data){
    interpolate3x3( (treeNode *) data, x,y,z, bx, by, bz); /*for now just ignore time*/
  }

%}


DECLARE
%{
  /*   Larmor frequency and scalar product threshold*/
  double gz;
  void *parPtr;
  mcmagnet_field_info *magnet;  
  vertex **vertices, **vertices2;
  treeNode *tree;
%}

INITIALIZE
%{  
  double gx, gy;
  Coords localG = rot_apply(ROT_A_CURRENT_COMP, coords_set(0,-GRAVITY,0));

  if (fieldFunction==const_magnetic_field){
    double *t=malloc(3*sizeof(double));
    t[0]=Bx;
    t[1]=By;
    t[2]=Bz;
    parPtr=(void *)t;
  } else if (fieldFunction==rot_magnetic_field){
    double *t=malloc(2*sizeof(double));
    t[0]=By;
    t[1]=zdepth;
    parPtr=(void *)t;
  } else if (fieldFunction==majorana_magnetic_field){
    double *t=malloc(3*sizeof(double));
    t[0]=Bx;
    t[1]=By;
    t[2]=zdepth;
    parPtr=(void *)t;
  } else if (fieldFunction==table_magnetic_field){
    /*initialize the interpolation vertex structure*/
    int r=-1;
    vertices=kdtree_loadPoints(filename, &r);
    printf("rows read %d\n",r);
    if (vertices==NULL || r < 0) {
      fprintf(stderr, "Error (%s): Failed to read magnetic field table file (r=%d): %s. Aborting\n",NAME_CURRENT_COMP,r, filename);
      exit(1);
    }
    /*resampling to double number of points*/
    vertices2 = resample(vertices, r, r);
    tree = kdtree_addToTree(vertices2, 0, 2*r-1, 0);
    parPtr=(void *) tree;
  }

  coords_get(localG, &gx, &gy, &gz);
  if(!mcgravitation)
    gz = 0;
  
  if ((xwidth<=0) || (yheight<=0) || (zdepth<=0)) {
    fprintf(stderr, "Pol_filter: %s: Null or negative volume!\n"
	    "ERROR      (xwidth, yheight, zdepth). Exiting\n",
	    NAME_CURRENT_COMP);
    exit(1);
  }  
%}

TRACE
%{
  //enter
  PROP_Z0;
  /*terminate neutrons which miss the component*/
  if (2*x>xwidth || 2*x<-xwidth || 2*y>yheight || 2*y<-yheight){
    ABSORB;
  }
  mcmagnet_push(fieldFunction,&(ROT_A_CURRENT_COMP),&(POS_A_CURRENT_COMP),0,parPtr); 
//  mcmagnet_print_stack();
%}

/* This section is executed when the simulation ends (C code). Other    */
/* optional sections are : SAVE                                         */
FINALLY
%{
%}


MCDISPLAY
%{
  magnify("xy");
  rectangle(0,0,0,xwidth,yheight,0);
%}

END
